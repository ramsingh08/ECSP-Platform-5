FUNCTION_BLOCK "FB_Host_Recv_Ext"
TITLE =%version: 0.04 %  CN: 40
//Function:
//Receive messages via TCP/IP Communication.
//
//History:
//This version  %created_by: nlhdl %
//              %date_created: Tuesday, January 12, 2016 9:20:40 AM %
//              %release: VI_RAW/RA_Dev %
//
//Modification History:                                   By:    Date:     Rev:
//----------------------------------------------------  -------  --------  -----
//ra#9909: Connection ID Snd/Rcv block type word        nlHdL    12-01-16   0.04
//ra#9751: Add source files ECS-P to VI_RAW             nlHdL    14-12-15   0.03
//ra#9751: Add source files ECS-P to VI_RAW             nlHdL    07-12-15   0.03
//7530: Chaneg MD_DWORD_To_Do to AreaID as report ID    nlHdL    20-07-15   0.03
//7324: ECS-P General from the RD to RA for platform    nlHdL    06-07-15   0.02
//22337: Remove the vibes1.x with the vibes 3.x         nlHdL    08-01-15   0.02
//Initial version, based on LVT KIEV                    nlHdL    01-10-13   0.01
//
//Description:
//This FB receives data using the Siemens AG_Send module. The FB can only 
//be used with a S7-300 PLC with an external network card.
//
//
FAMILY : VIBES
VERSION : 0.0


VAR_INPUT
  i_ID_Connection : WORD ;	//ID Connection number (NETPRO -> Connection properties -> Block parameters)
  i_LADDR : WORD ;	//Module starting address (NETPRO -> Connection properties -> Block parameters)
  i_DB_Recv_FIFO : BLOCK_DB ;	//Data Structure: TCTC DB receive FIFO
END_VAR
VAR_OUTPUT
  o_Connection_Error : BOOL ;	//Connection error
END_VAR
VAR
  s_Received_Message : "UDT_Msg_Rcv_Layout";	
  s_Received_Message_End : BOOL ;	//Received message end (to calculate length) 
  s_Message_Length_Fault : BOOL ;	//Wrong message length received
  s_Header_Fault : BOOL ;	//Wrong header received (STX)
  s_Footer_Fault : BOOL ;	//Wrong footer received (ETX)
  s_Message_Received : BOOL ;	//When TRUE a message has been received this scan
  s_RCV_Error : BOOL ;	//An error occured while trying to receive a message
  s_Received_Byte : BYTE ;	//Received byte used for resynchronisation of the receive buffer
  s_FF_Counted : INT ;	//Counter for received byte is "FF"
  s_RCV_Status : WORD ;	//Status code of receive block
END_VAR
VAR_TEMP
  t_STATUS : WORD ;	//The status of the receive module
  t_LEN : INT ;	//Number of number of bytes received
  t_Specified_Msg_Length : INT ;	//Specified message length
  t_Ret_Val_Word : WORD ;	//Return value
  t_DONE : BOOL ;	//Indicates if receive module has finished receiveing
  t_ERROR : BOOL ;	//Indicates an error with the receive module
  t_Message_OK : BOOL ;	//Message layout is checked OK
  t_OS_Message_Received : BOOL ;	//One-shot new message received
  t_DB_Num : INT ;	//DB number of receive FIFO
  t_ID_Event : DWORD ;	//The event ID for report
  t_ID_Connection : INT ;	//ID Connection as integer
END_VAR
BEGIN
NETWORK
TITLE =AA: Resynchronisation of receive buffer
//During a footer fault (e.g. last four bytes did not contain FF) the receive 
//buffer is emptied byte for byte until four subsequent bytes, filled with FF, 
//are 
//found.
      L     #i_ID_Connection; 
      T     #t_ID_Connection; 

      A     #s_Footer_Fault; // IF Footer fault active
      JCN   AA99; 

      CALL "AG_Receive_S7_300" (// THEN take 1 byte out receive buffer
           ID                       := #t_ID_Connection,
           LADDR                    := #i_LADDR,
           RECV                     := #s_Received_Byte,
           NDR                      := #t_DONE,
           ERROR                    := #t_ERROR,
           STATUS                   := #t_STATUS,
           LEN                      := #t_LEN);

// If recieve action not finished then end block
      AN    #t_DONE; 
      BEC   ; 

// If last byte contains FF then increase s_FF_Counted.
// Else reset s_FF_Counted.
      L     #s_Received_Byte; 
      L     B#16#FF; 
      ==I   ; 
      JCN   AA01; 

      L     #s_FF_Counted; 
      L     1; 
      +I    ; 
      T     #s_FF_Counted; 

      JU    AA02; 

AA01: L     0; 
      T     #s_FF_Counted; 

// If last four bytes contained FF then reset footer fault.
AA02: L     4; 
      L     #s_FF_Counted; 
      <=I   ; 
      R     #s_Footer_Fault; // THEN Footer fault still active
      BEU   ; // End block

AA99: NOP   0; 
NETWORK
TITLE =BA: Receive message
//IF the CP has received a message AND the last message was read by the 
//application THEN the message is put in s_Received_Message. 
      CALL "AG_Receive_S7_300" (
           ID                       := #t_ID_Connection,
           LADDR                    := #i_LADDR,
           RECV                     := #s_Received_Message,
           NDR                      := #t_DONE,
           ERROR                    := #t_ERROR,
           STATUS                   := #t_STATUS,
           LEN                      := #t_LEN);
NETWORK
TITLE =CA: Error detection/handling, receiving
//Check for an error of the AG_LReceive module and handle the error.
      AN    #t_ERROR; // IF No error anymore
      A     #t_DONE; 
      JCN   CA01; 

      R     #s_RCV_Error; // THEN reset receive error

      L     0; 
      T     #s_RCV_Status; // AND reset the status word

CA01: NOP   0; 

      AN    #t_DONE; // IF an error has occured while trying to receive a message..
      A     #t_ERROR; 
      S     #s_RCV_Error; // THEN set the error output bit..
      JCN   CA99; 

      L     #t_STATUS; 
      T     #s_RCV_Status; // AND output the status word

CA99: NOP   0; 
NETWORK
TITLE =DA: Calculate message length

      L     P##s_Received_Message_End; // Pointer to end of 'Received message'
      L     P##s_Received_Message; // Pointer to begin of 'Received message'
      -D    ; // Length in bits
      SRD   3; // Record length of 'Received message' in bytes
      T     #t_Specified_Msg_Length; // Copy Length to t_Specified_Msg_Length
NETWORK
TITLE =EA: New message received

      A     #t_DONE; //IF receive action is done
      AN    #t_ERROR; //AND no communication error occurred
      =     #t_OS_Message_Received; 
      JCN   EA99; //THEN...
//Check message length
      L     #t_Specified_Msg_Length; 
      L     #t_LEN; 
      <>I   ; 
      =     #s_Message_Length_Fault; 

//Check message header
      L     #s_Received_Message.Header.Byte_1; 
      L     B#16#2; 
      ==I   ; 
      JCN   EA01; 

      L     #s_Received_Message.Header.Byte_2; 
      L     B#16#0; 
      ==I   ; 
      JC    EA02; 

EA01: SET   ; 
      =     #s_Header_Fault; 
      JU    EA03; 
EA02: CLR   ; 
      =     #s_Header_Fault; 
EA03: NOP   0; 


//Check message footer
      L     #s_Received_Message.Footer.Byte_1; 
      L     B#16#FF; 
      ==I   ; 
      JCN   EA04; 

      L     #s_Received_Message.Footer.Byte_2; 
      L     B#16#FF; 
      ==I   ; 
      JCN   EA04; 

      L     #s_Received_Message.Footer.Byte_3; 
      L     B#16#FF; 
      ==I   ; 
      JCN   EA04; 

      L     #s_Received_Message.Footer.Byte_4; 
      L     B#16#FF; 
      ==I   ; 
      JC    EA05; 

EA04: SET   ; 
      =     #s_Footer_Fault; 
      JU    EA06; 
EA05: CLR   ; 
      =     #s_Footer_Fault; 
EA06: NOP   0; 

//Is message OK?
      AN    #s_Message_Length_Fault; 
      AN    #s_Header_Fault; 
      AN    #s_Footer_Fault; 
      =     #t_Message_OK; 

EA99: NOP   0; 
NETWORK
TITLE =FA: Copy message to receive buffer

      A     #t_OS_Message_Received; //IF new message received
      A     #t_Message_OK; //AND message layout checked OK
      JCN   FA01; 

      OPN   #i_DB_Recv_FIFO; // Determine receive FIFO's DB nr
      L     DBNO; 
      T     #t_DB_Num; // And store on a temp of the type INT

      L     "DB_Outgoing_Msgs".LifeSignRequest.AreaID; 
      DTB   ; 
      SLD   16; 
      T     #t_ID_Event; 

      CALL "FC_FIFO" (
           i_FIFO_DB_Num            := #t_DB_Num,
           i_Mode                   := 'L',
           i_ANY_Data               := #s_Received_Message.Body,
           i_ID                     := #t_ID_Event,
           o_RET_VAL_FIFO           := #t_Ret_Val_Word);

FA01: NOP   0; 
NETWORK
TITLE =GA: Call AG_LRecv_S7_300

      A     #s_Message_Received; 
      JCN   GA99; 

      CALL "AG_Receive_S7_300" (
           ID                       := #t_ID_Connection,
           LADDR                    := #i_LADDR,
           RECV                     := #s_Received_Message,
           NDR                      := #t_DONE,
           ERROR                    := #t_ERROR,
           STATUS                   := #t_STATUS,
           LEN                      := #t_LEN);

GA99: NOP   0; 
NETWORK
TITLE =HA: Create error indication to Send block

      L     #t_STATUS; 
      L     W#16#80A4; // The communication bus connection between the CPU and CP is not established.
      ==I   ; 
      O(    ; 
      L     #t_STATUS; 
      L     W#16#80C4; // Communications error
      ==I   ; 
      )     ; 
      =     #o_Connection_Error; 
END_FUNCTION_BLOCK

