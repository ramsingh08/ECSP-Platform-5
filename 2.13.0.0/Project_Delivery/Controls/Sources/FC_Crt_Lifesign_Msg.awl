FUNCTION "FC_Crt_Lifesign_Msg" : VOID
TITLE =%version: 0.05 % CN: 32
//Function:
//Create lifesign message for TCTC interface and copy complete message into 
//the FIFO DB.
//
//History:
//This version  %created_by:  nlhdl %
//              %date_created:  maandag 7 december 2015 16:06:18 %
//              %release: VI_RAW/RA_Dev %
//
//Modification History:                                   By:    Date:     Rev:
//----------------------------------------------------  -------  --------  -----
//ra#9751: Add source files ECS-P to VI_RAW             nlHdL    14-12-15   0.05
//ra#9751: Add source files ECS-P to VI_RAW             nlHdL    07-12-15   0.05
//8673: Reset:Restart for errors via  PNPN interface    nlHdL    29-10-15   0.05
//7632: Add blocks for platform 4. PNPN Coupler         nlHdL    30-07-15   0.04
//23824: Remove station ID                              nlMlaut  29-04-15   0.04
//20899: remove filler from PNPN Lifesign               nlHdL    16-09-14   0.03
//15807: Block consistency update IO symbols, comment   nlHdL    18-09-13   0.02
//Initial Revision                                      nlHHop   17-04-13   0.01
//
//Description:
//The lifesign message contains a message header and a message body like 
//displayed below.
//Message header:
// -> Event time
// -> Message type
// -> Message identifier
//Message body:
// -> Sequence number
// -> Filler
//
FAMILY : General
VERSION : 0.0


VAR_INPUT
  i_ID : DWORD ;	//ID Record HEX AAAA - ZZSS/OO.
  i_FIFO_TCTC_DB_num : INT ;	//Number of DB TCTC FIFO
  i_Message_ID : INT ;	//200 = Lifesign request, 201 = Lifesign response
  i_Sequence_Number : INT ;	//Sequence number to be used in this message
END_VAR
VAR_TEMP
  t_Ret_Val_Int : INT ;	//Return value Integer, not Used
  t_Ret_Val_Word : WORD ;	//Return value Word, not used
END_VAR
BEGIN
NETWORK
TITLE =AA: Set message header
//In this network the message header is created and copied into 
//DB_PNPN_OutgoingMsgs.
//
//Message header:
// -> Event time [DATE_AND_TIME]
// -> Message type [INT]
// -> Message identifier [DINT]
      NOP   0; 
// Copy date and time into message header
      CALL "READ_CLK" (
           RET_VAL                  := #t_Ret_Val_Int,
           CDT                      := "DB_PNPN_OutgoingMsgs".Lifesign_Message.Header.EventTime);

// Copy message type into message header (200 = Lifesign request, 201 = Lifesign response)
      L     #i_Message_ID; 
      T     "DB_PNPN_OutgoingMsgs".Lifesign_Message.Header.Message_Type; 

// Clear the message identifier field
      L     0; 
      T     "DB_PNPN_OutgoingMsgs".Lifesign_Message.Header.Message_Identifier; 


NETWORK
TITLE =BA: Copy sequence number to message
//Copy sequence number to DB_PNPN_OutgoingMsgs.
      L     #i_Sequence_Number; 
      T     "DB_PNPN_OutgoingMsgs".Lifesign_Message.Sequence_Number; 

NETWORK
TITLE =CA: Put message in buffer
//Copy complete message into FIFO DB.
      CALL "BLKMOV" (
           SRCBLK                   := "DB_PNPN_OutgoingMsgs".Default,
           RET_VAL                  := #t_Ret_Val_Int,
           DSTBLK                   := "DB_PNPN_OutgoingMsgs".Merged_Message);

      CALL "BLKMOV" (
           SRCBLK                   := "DB_PNPN_OutgoingMsgs".Lifesign_Message,
           RET_VAL                  := #t_Ret_Val_Int,
           DSTBLK                   := "DB_PNPN_OutgoingMsgs".Merged_Message);

      CALL "FC_FIFO" (
           i_FIFO_DB_Num            := #i_FIFO_TCTC_DB_num,
           i_Mode                   := 'I',
           i_ANY_Data               := "DB_PNPN_OutgoingMsgs".Merged_Message,
           i_ID                     := #i_ID,
           o_RET_VAL_FIFO           := #t_Ret_Val_Word);

END_FUNCTION

