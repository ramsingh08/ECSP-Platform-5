FUNCTION "FC_Crt_OPS_ModeReq" : VOID
TITLE =%version: 0.02 % CN: 32
//Function:
//Create ModeRequest message for OPS station and write message to
//the FIFO DB.
//
//History:
//This version  %created_by:  nljclo %
//              %date_created:  Wednesday, September 21, 2016 2:31:17 PM %
//              %release: VI_GEN/RA_Dev %
//
//Modification History:                                   By:    Date:     Rev:
//----------------------------------------------------  -------  --------  -----
//ra#17264: Moved from RAW to GEN                       nlJClo   21-09-16   0.02
//ra#16996: Update the OPZ according MCFV decomposition nlJClo   09-09-16   0.02
//ra#16996: Update the OPZ according MCFV decomposition nlJClo   09-09-16   0.02
//24455: Cancel requested window OPS mess Area ID       nlHdL    29-06-15   0.02
//Initial Revision                                      nlHdL    29-04-15   0.01
//
//Description:
//The ModeRequest message contains a message header and a message body like 
//displayed below.
//Message header:
// -> Event time
// -> Message type
// -> Message identifier
// -> Sending area ID
//Message body:
// -> OperationalMode 2= system
// -> Functionmode
// -> Filler
//
FAMILY : GEN
VERSION : 0.0


VAR_INPUT
  i_ID : DWORD ;	//ID Record HEX AAAA - ZZSS/OO.
  i_FIFO_TCTC_DB_num : INT ;	//Number of DB TCTC FIFO
  i_OperationalMode : INT ;	//Operational Mode 1 - System 2 - Degraded
  i_FunctionMode : WORD ;	//Function Mode for the message
  i_DB_Event_Num : INT ;	//Number of DB event
END_VAR
VAR_TEMP
  t_Ret_Val_Int : INT ;	//Return value Integer, not Used
  t_Ret_Val_Word : WORD ;	//Return value Word, not used
END_VAR
BEGIN
NETWORK
TITLE =AA: Set message header
//In this network the message header is created and copied into 
//DB_OPS_OutgoingMsgs.
//
//Message header:
// -> Event time [DATE_AND_TIME]
// -> Message type [INT]
// -> Message identifier [DINT]
// -> Sending area ID [DINT]
      NOP   0; 
// Copy date and time into message header
      CALL "READ_CLK" (
           RET_VAL                  := #t_Ret_Val_Int,
           CDT                      := "DB_OPS_Outgoing_Msgs".Mode_Req_Message.Header.EventTime);


// Copy message type into message header 
      L     53; 
      T     "DB_OPS_Outgoing_Msgs".Mode_Req_Message.Header.Message_Type_ID; 

// Copy the Sending Area ID into the message header
      L     #i_ID; // Load ID
      L     DW#16#FFFF0000; // Filter Area number only
      AD    ; 
      SRD   16; // Slide Area nubmer to beginning AAAA0000 -> AAAA
      BTD   ; // Convert HEX to INT
      T     "DB_OPS_Outgoing_Msgs".Mode_Req_Message.Header.Source_ID; // Transfer to outgoing messages

NETWORK
TITLE =BA: Create message Body
//Put requested Mode in Message
      L     #i_OperationalMode; // Operational mode 1 - System 2 - Degraded
      T     "DB_OPS_Outgoing_Msgs".Mode_Req_Message.Msg_Body_ModeRequest.OperationalMode; 


      L     #i_FunctionMode; // Function Mode
      T     "DB_OPS_Outgoing_Msgs".Mode_Req_Message.Msg_Body_ModeRequest.FunctionMode; 

NETWORK
TITLE =CA: Put message in buffer
//Copy complete message into FIFO DB.
      CALL "BLKMOV" (
           SRCBLK                   := "DB_OPS_Outgoing_Msgs".Default,
           RET_VAL                  := #t_Ret_Val_Int,
           DSTBLK                   := "DB_OPS_Outgoing_Msgs".Merged_Message);

      CALL "BLKMOV" (
           SRCBLK                   := "DB_OPS_Outgoing_Msgs".Mode_Req_Message,
           RET_VAL                  := #t_Ret_Val_Int,
           DSTBLK                   := "DB_OPS_Outgoing_Msgs".Merged_Message);

      CALL "FC_FIFO" (
           i_FIFO_DB_Num            := #i_FIFO_TCTC_DB_num,
           i_Mode                   := 'I',
           i_ANY_Data               := "DB_OPS_Outgoing_Msgs".Merged_Message,
           i_ID                     := #i_ID,
           o_RET_VAL_FIFO           := #t_Ret_Val_Word);

END_FUNCTION

