FUNCTION_BLOCK "FB_Running_Average"
TITLE =%version: 0.02 % CN: 30
//Function:
//This function block will calculate the running average
//
//History:
//This version  %created_by: nlhdl %
//              %date_created: maandag 7 december 2015 16:01:07 %
//              %release: VI_RAW/RA_Dev %
//
//Modification History:                                   By:    Date:     Rev:
//----------------------------------------------------  -------  --------  -----
//ra#9751: Add source files ECS-P to VI_RAW             nlHdL    14-12-15   0.02
//ra#9751: Add source files ECS-P to VI_RAW             nlHdL    07-12-15   0.02
//7324: ECS-P General from the RD to RA for platform    nlHdL    06-07-15   0.02
//15807: Block consistency update IO symbols, comment   nlHdL    18-09-13   0.02
//Copied from SSS                                       nlHdL    13-04-10   0.01
//Based on version 1.2 of the SSS General Library
//
//Description:
//Calculate the running average on the basis of the 
//following formula:
//
//                    {New Value} + {Current * (Factor-1)}
//Running Average :=  ------------------------------------
//                                 {Factor}             
//
//Current = running average previous occurrence
//Factor = 1,2,3,4.......i_Factor
//
FAMILY : General
VERSION : 0.0


VAR_INPUT
  i_New_Value : INT ;	//New value
  i_Factor : INT ;	//Weighting factor
  i_Restart : BOOL ;	//Restart running average calculation
END_VAR
VAR_OUTPUT
  o_Running_Average : INT ;	//New value running average Int
  o_Running_Average_Real : REAL ;	//New value running average Real
END_VAR
VAR
  s_Factor : INT  := 1;	//Factor
END_VAR
VAR_TEMP
  t_Real_A : REAL ;	//Value A Real
  t_Real_B : REAL ;	//Value B Real
  t_Real_C : REAL ;	//Value C Real
  t_Factor : REAL ;	//Factor Real
END_VAR
BEGIN
NETWORK
TITLE =AA: Reset running average calculation
//If input reset is TRUE the running average is set to 0 and the factor to 1
      A     #i_Restart; 
      JCN   AA99; 
      L     0.000000e+000; 
      T     #o_Running_Average_Real; 
      L     1; 
      T     #s_Factor; 
AA99: NOP   0; 
NETWORK
TITLE =BA: Determine new running average
//                   {New} + {Current * (Factor-1)}    {A} + {B}
//Running Average :=  ------------------------------  = ---------
//                              {Factor}                  {C} 
//
//Calculation is done in Reals, because of Roundings in Integer calculations.
      NOP   0; 
// Determination value A
      L     #i_New_Value; 
      ITD   ; 
      DTR   ; 
      T     #t_Real_A; //= {A}

// Determination value B
      L     #s_Factor; 
      ITD   ; 
      DTR   ; 
      T     #t_Factor; 
      L     1.000000e+000; 
      -R    ; 
      L     #o_Running_Average_Real; 
      *R    ; 
      T     #t_Real_B; //= {B}

// Determination value C
      L     #t_Factor; 
      T     #t_Real_C; //= {C}

// Determination running average
      L     #t_Real_A; 
      L     #t_Real_B; 
      +R    ; 
      L     #t_Real_C; 
      /R    ; 
      T     #o_Running_Average_Real; //= New Running Average Real
      RND   ; 
      T     #o_Running_Average; //= New Running Average Int
NETWORK
TITLE =CA: Determine New factor

      NOP   0; 
// Input factor always >= 1
      L     #i_Factor; // IF input Factor <= 0
      L     0; 
      <=I   ; 
      JCN   CA10; 
      L     1; // THEN input Factor = 1
      T     #i_Factor; 

// Determine new weighing factor
CA10: L     #s_Factor; // IF Factor < Weighting Factor
      L     #i_Factor; 
      <I    ; 
      JCN   CA01; // THEN Factor = Factor + 1
      L     #s_Factor; 
      L     1; 
      +I    ; 
      T     #s_Factor; 
      JU    CA99; 
CA01: T     #s_Factor; // ELSE Factor = Weigthing Factor
CA99: NOP   0; 
END_FUNCTION_BLOCK

