FUNCTION "FC_Crt_DeregisterTub" : VOID
TITLE =%version: 0.04 %  CN: 32
//Function:
//Prepare and send message 'DeregisterPackage'
//
//History:
//This version  %created_by: nlhdl %
//              %date_created: maandag 7 december 2015 16:06:10 %
//              %release: VI_RAW/RA_Dev %
//
//Modification History:                                 By:      Date:     Rev:
//----------------------------------------------------  -------  --------  -----
//ra#9751: Add source files ECS-P to VI_RAW             nlHdL    14-12-15   0.04
//ra#9751: Add source files ECS-P to VI_RAW             nlHdL    07-12-15   0.04
//7324: ECS-P General from the RD to RA for platform    nlHdL    06-07-15   0.04
//23155: TIR in message for tubtrace                    nlHdL    24-04-15   0.04
//22337: Remove the vibes1.x with the vibes 3.x         nlHdL    09-01-15   0.03
//19518: Block call update of FC_xxx_Record             nltny    07-08-14   0.02
//18060: Update messages ID's for Vibes 3.2             nlHdL    01-04-14   0.01
//Initial Version based on version 0.04                 nlHdL    13-12-13   0.01
//
//Description:
//Prepare and send message 'DeregisterPackage'
//
FAMILY : VIBES
VERSION : 0.0


VAR_INPUT
  i_ID : DWORD ;	//ID Record HEX AAAA - ZZSS/OO.
  i_Data : INT ;	//Data needed to send the message
  i_FIFO_PLC_DB_num : INT ;	//Number of DB PLC FIFO
  i_DB_Event_Num : INT ;	//Number of DB event
  i_Reason : INT ;	//1= End of Tracing, 2= Missing
END_VAR
VAR_TEMP
  t_AR1_Backup : DINT ;	//Save the contents of AR1
  t_Ret_Val_Word : WORD ;	//Return value Word, not used
  t_Ret_Val_Int : INT ;	//Return value Integer, not Used
  t_LIC : INT ;	//LIC number
  t_Reason_Missing : BOOL ;	
  t_TIC_Number : INT ;	//TIC number
  t_Reason_Hand_Over : BOOL ;	
  t_Reason_End_Tracing : BOOL ;	
END_VAR
BEGIN
NETWORK
TITLE =AA: Get Date and Time

      TAR1  ; 
      T     #t_AR1_Backup; 

      CALL "READ_CLK" (
           RET_VAL                  := #t_Ret_Val_Int,
           CDT                      := "DB_Outgoing_Msgs".DeRegister.Body.EventTime);

NETWORK
TITLE =CA: Write messageID

      AN    "M_Comm_Degraded"; 
      JCN   CA01; 

      L     500; // When NOT in degraded mode
      T     "DB_Outgoing_Msgs".DeRegister.Body.MessageID; 

      JU    CA99; 

CA01: L     1500; // When in degraded mode
      T     "DB_Outgoing_Msgs".DeRegister.Body.MessageID; 

CA99: NOP   0; 
NETWORK
TITLE =DA: Copy AreaID, ZoneID and SectionID

      L     #i_ID; // Load ID
      L     DW#16#FFFF0000; // Filter Area number only
      AD    ; 
      SRD   16; // Slide Area nubmer to beginning AAAA0000 -> AAAA
      BTD   ; // Convert HEX to INT
      T     "DB_Outgoing_Msgs".DeRegister.Body.AreaID; // Transfer to outgoing messages

      L     #i_ID; // Load ID
      L     DW#16#FF00; // Filter Zone number only
      AD    ; 
      SRD   8; // Slide Zone nubmer to beginning 0000ZZ00 -> 0000ZZ
      BTI   ; // Convert HEX to INT
      T     "DB_Outgoing_Msgs".DeRegister.Body.ZoneID; // Transfer to outgoing messages

      L     #i_ID; // Load ID
      L     DW#16#FF; // Filter Section number only
      AD    ; 
      BTI   ; // Convert HEX to INT
      T     "DB_Outgoing_Msgs".DeRegister.Body.EquipmentID; // Transfer to outgoing messages
NETWORK
TITLE =EA: Write LIC Number in message

      CALL "FC_Read_Int_Record" (
           i_Record_Number          := #i_Data,
           i_P_INT_Record           := "DB_UDT_LNK_Record".LIC,
           i_DB                     := "DB_LNK_List",
           i_DB_Event_Num           := #i_DB_Event_Num,
           o_INT                    := #t_LIC);

      L     #t_LIC; 
      T     "DB_Outgoing_Msgs".DeRegister.Body.LIC; 

      CALL "FC_Read_ANY_Record" (
           i_Record_Number          := #t_LIC,
           i_ANY_Data_Record        := "DB_UDT_LIC_Record".BIR,
           i_ANY_Data               := "DB_Outgoing_Msgs".DeRegister.Body.BIR,
           i_DB                     := "DB_LIC_List",
           i_DB_Event_Num           := #i_DB_Event_Num);

      L     #i_Reason; 
      T     "DB_Outgoing_Msgs".DeRegister.Body.Reason; 

      L     "DB_Outgoing_Msgs".DeRegister.Body.BIR.PID; 
      L     0; 
      ==D   ; 
      JCN   EA01; 

      L     2206; // When PID = 0 then send log message deregister no pid
      T     "DB_Outgoing_Msgs".DeRegister.Body.MessageID; 

EA01: NOP   0; 
      L     0; 
      T     "DB_Outgoing_Msgs".DeRegister.Body.TIR.ID; 

      L     "DB_Setting".common.External_Log_Level; 
      L     4; 
      >I    ; 
      JCN   EA02; 

      CALL "FC_Read_Int_Record" (
           i_Record_Number          := #i_Data,
           i_P_INT_Record           := "DB_UDT_LNK_Record".TIC,
           i_DB                     := "DB_LNK_List",
           i_DB_Event_Num           := #i_DB_Event_Num,
           o_INT                    := #t_TIC_Number);

      CALL "FC_Read_ANY_Record" (
           i_Record_Number          := #t_TIC_Number,
           i_ANY_Data_Record        := "DB_UDT_TIC_Record".TIR,
           i_ANY_Data               := "DB_Outgoing_Msgs".DeRegister.Body.TIR,
           i_DB                     := "DB_TIC_List",
           i_DB_Event_Num           := #i_DB_Event_Num);

EA02: NOP   0; 
NETWORK
TITLE =FA: Put message in buffer

      CALL "BLKMOV" (
           SRCBLK                   := "DB_Outgoing_Msgs".Default,
           RET_VAL                  := #t_Ret_Val_Int,
           DSTBLK                   := "DB_Outgoing_Msgs".Merged_Message);

      CALL "BLKMOV" (
           SRCBLK                   := "DB_Outgoing_Msgs".DeRegister,
           RET_VAL                  := #t_Ret_Val_Int,
           DSTBLK                   := "DB_Outgoing_Msgs".Merged_Message);

      CALL "FC_FIFO" (
           i_FIFO_DB_Num            := #i_FIFO_PLC_DB_num,
           i_Mode                   := 'L',
           i_ANY_Data               := "DB_Outgoing_Msgs".Merged_Message,
           i_ID                     := #i_ID,
           o_RET_VAL_FIFO           := #t_Ret_Val_Word);

NETWORK
TITLE =GA: Restore AR1

      L     #t_AR1_Backup; 
      LAR1  ; 
END_FUNCTION

