FUNCTION "FC_Route_Default_Dir" : VOID
TITLE =%version: 0.07 % CN: 33
//Function:
//Check if decision point is in default mode (No Routing table)
//Write preffered and alternative direction in the destination
//
//History:
//This version  %created_by: inrjoi %
//              %date_created: Thursday, September 07, 2017 6:59:03 AM %
//              %release: VI_GEN/RA_Dev %
//
//Modification History:                                   By:    Date:     Rev:
//----------------------------------------------------  -------  --------  -----
//ra#23996: PLC going to STOP when used in UNZ01        inrjoi   07-09-17   0.07
//ra#8938: Extra direction decision point and routing   nlHdL    23-02-16   0.06
//ra#9751: Add source files ECS-P to VI_RAW             nlHdL    14-12-15   0.05
//7324: ECS-P General from the RD to RA for platform    nlHdL    06-07-15   0.05
//15807: Block consistency update IO symbols, comment   nlHdL    18-09-13   0.05
//PR11918: Adopted Decision point update with Dir A & B nlHdL    02-04-13   0.04
//CR19512 Update sources according S7 Manager           nlHdL    08-03-13   0.03
//PR11727 Add synergy key words 'created_by' and date   nlHdL    14-11-12   0.02
//Initial Revision                                      nlHdL    03-10-11   0.01
//
//Description:
//Check if decision point is in default mode (No Routing table)
//Write preffered and alternative direction in the destination
//
FAMILY : General
VERSION : 0.0


VAR_INPUT
  i_CFG_Dir_A_Preference : INT ;	//CFG: [Dir] Preference for Direction A (0 = No Direction 1 = Preferred)
  i_CFG_Dir_B_Preference : INT ;	//CFG: [Dir] Preference for Direction B (0 = No Direction 1 = Preferred)
  i_CFG_Dir_C_Preference : INT ;	//CFG: [Dir] Preference for Direction C (0 = No Direction 1 = Preferred)
  i_ANY_Direction_A : ANY ;	//ANY pointer to the Direction A messages
  i_ANY_Direction_B : ANY ;	//ANY pointer to the Direction B messages
  i_ANY_Direction_C : ANY ;	//ANY pointer to the Direction C messages
  i_DB_Event_Num : INT ;	//Number of DB event
  i_Rep_Route : INT ;	//Replay Route message for tracking report
  i_CFG_Dir_C : BOOL ;	//Included direction C
END_VAR
VAR_TEMP
  t_Preference_A : BYTE ;	//Preference for Direction A
  t_Preference_B : BYTE ;	//Preference for Direction B
  t_Preference_C : BYTE ;	//Preference for Direction C
  t_DecisionLvl : BYTE ;	//Decision Level	
  t_FailedReason_A : BYTE ;	//Failed Reason for Direction A	
  t_FailedReason_B : BYTE ;	//Failed Reason for Direction B
  t_FailedReason_C : BYTE ;	//Failed Reason for Direction C
  t_Failed1 : BYTE ;	//Failed Direction A
  t_Failed2 : BYTE ;	//Failed Direction B
END_VAR
BEGIN
NETWORK
TITLE =AA: Check Default Routing A

      L     0; 
      T     #t_DecisionLvl; 
      T     #t_FailedReason_A; 
      T     #t_FailedReason_B; 
      T     #t_FailedReason_C; 
      T     #t_Failed1; 
      T     #t_Failed2; 

      L     0; 
      L     #i_CFG_Dir_A_Preference; 
      ==I   ; 
      JCN   AA01; 

      L     1; 
      T     #t_DecisionLvl; 

      L     1; 
      T     #t_FailedReason_A; 
      T     #t_Failed1; 

      JU    AA03; 
//If no default is set FailedReason = 1 ("NO ROUTING DEFAULT")

AA01: L     #i_CFG_Dir_A_Preference; 
      L     0; 
      >I    ; 
      JCN   AA03; 

      L     1; 
      T     #t_DecisionLvl; 

      L     0; 
      L     #i_CFG_Dir_B_Preference; 
      ==I   ; 
      JCN   AA02; 

      L     1; 
      T     #t_FailedReason_B; 
//If no alternative set FailedReason = 1 ("NO ROUTING DEFAULT")

AA02: A     #i_CFG_Dir_C; 
      JCN   AA03; 

      L     1; 
      T     #t_DecisionLvl; 

      L     0; 
      L     #i_CFG_Dir_C_Preference; 
      ==I   ; 
      JCN   AA03; 

      L     1; 
      T     #t_FailedReason_C; 
//If no alternative set FailedReason = 1 ("NO ROUTING DEFAULT")

AA03: NOP   0; 
      L     #i_CFG_Dir_A_Preference; 
      T     #t_Preference_A; 

      L     #i_CFG_Dir_B_Preference; 
      T     #t_Preference_B; 

      L     #i_CFG_Dir_C_Preference; 
      T     #t_Preference_C; 

      L     P##i_ANY_Direction_A; 
      LAR1  ; 

      L     D [AR1,P#6.0]; // Byte6=memory area, Byte7..9=Byte,Bit address
      LAR2  ; 

      L     0; 
      T     DIW [AR2,P#0.0]; 

      L     #t_Preference_A; 
      T     DIB [AR2,P#2.0]; 
      L     #t_Preference_B; 
      T     DIB [AR2,P#3.0]; 
      L     #t_DecisionLvl; 
      T     DIB [AR2,P#5.0]; 
      L     #t_FailedReason_A; 
      T     DIB [AR2,P#6.0]; 
      L     #t_FailedReason_B; 
      T     DIB [AR2,P#7.0]; 

      A     #i_CFG_Dir_C; 
      JCN   AA99; 

      L     #t_Preference_C; 
      T     DIB [AR2,P#4.0]; 
      L     #t_FailedReason_C; 
      T     DIB [AR2,P#8.0]; 

AA99: NOP   0; 
NETWORK
TITLE =BA: Check Default Routing B

      L     0; 
      T     #t_DecisionLvl; 
      T     #t_FailedReason_A; 
      T     #t_FailedReason_B; 
      T     #t_FailedReason_C; 

      L     0; 
      L     #i_CFG_Dir_B_Preference; 
      ==I   ; 
      JCN   BA01; 

      L     1; 
      T     #t_DecisionLvl; 

      L     1; 
      T     #t_FailedReason_B; 
      T     #t_Failed2; 

//If no default is set FailedReason = 1 ("NO ROUTING DEFAULT")

      L     #t_Failed1; 
      L     1; 
      ==I   ; 
      AN    #i_CFG_Dir_C; // Dir C not used
      JCN   BA02; 
//If A and B is failed then report Failed direction

      CALL "FC_Exec_Failed_Dir" (
           i_Rep_Route              := #i_Rep_Route,
           i_Failed_Direction       := 0,
           i_Failed_Reason          := 1,
           i_Failed_Destination     := 0,
           i_Failed_Decisionlvl     := 1,// Routing default
           i_DB_Event_Num           := #i_DB_Event_Num);

      JU    BA03; 

BA01: L     #i_CFG_Dir_B_Preference; 
      L     0; 
      >I    ; 
      JCN   BA02; 

      L     1; 
      T     #t_DecisionLvl; 

      L     0; 
      L     #i_CFG_Dir_A_Preference; 
      ==I   ; 
      JCN   BA02; 

      L     1; 
      T     #t_FailedReason_A; 
//If no alternative set FailedReason = 1 ("NO ROUTING DEFAULT")

//------------ Dir c -----------------------
BA02: A     #i_CFG_Dir_C; 
      JCN   BA03; 

      L     1; 
      T     #t_DecisionLvl; 

      L     0; 
      L     #i_CFG_Dir_C_Preference; 
      ==I   ; 
      JCN   BA03; 

      L     1; 
      T     #t_FailedReason_C; 
//If no alternative set FailedReason = 1 ("NO ROUTING DEFAULT")

BA03: L     #i_CFG_Dir_A_Preference; 
      T     #t_Preference_A; 

      L     #i_CFG_Dir_B_Preference; 
      T     #t_Preference_B; 

      L     #i_CFG_Dir_C_Preference; 
      T     #t_Preference_C; 

      L     P##i_ANY_Direction_B; 
      LAR1  ; 

      L     D [AR1,P#6.0]; // Byte6=memory area, Byte7..9=Byte,Bit address
      LAR2  ; 

      L     0; 
      T     DIW [AR2,P#0.0]; 

      L     #t_Preference_A; 
      T     DIB [AR2,P#2.0]; 
      L     #t_Preference_B; 
      T     DIB [AR2,P#3.0]; 
      L     #t_DecisionLvl; 
      T     DIB [AR2,P#5.0]; 
      L     #t_FailedReason_A; 
      T     DIB [AR2,P#6.0]; 
      L     #t_FailedReason_B; 
      T     DIB [AR2,P#7.0]; 

      A     #i_CFG_Dir_C; 
      JCN   BA99; 

      L     #t_Preference_C; 
      T     DIB [AR2,P#4.0]; 
      L     #t_FailedReason_C; 
      T     DIB [AR2,P#8.0]; 

BA99: NOP   0; 
NETWORK
TITLE =CA: Check Default Routing C

      AN    #i_CFG_Dir_C; // Dir C not used
      BEC   ; 

      L     0; 
      T     #t_DecisionLvl; 
      T     #t_FailedReason_A; 
      T     #t_FailedReason_B; 
      T     #t_FailedReason_C; 

      L     0; 
      L     #i_CFG_Dir_C_Preference; 
      ==I   ; 
      JCN   CA01; 

      L     1; 
      T     #t_DecisionLvl; 

      L     1; 
      T     #t_FailedReason_C; 

//If no default is set FailedReason = 1 ("NO ROUTING DEFAULT")

      L     #t_Failed1; 
      L     #t_Failed2; 
      +I    ; 
      L     2; 
      ==I   ; 
//If A B and c is failed then report Failed direction

      CALL "FC_Exec_Failed_Dir" (
           i_Rep_Route              := #i_Rep_Route,
           i_Failed_Direction       := 0,
           i_Failed_Reason          := 1,
           i_Failed_Destination     := 0,
           i_Failed_Decisionlvl     := 1,// Routing default
           i_DB_Event_Num           := #i_DB_Event_Num);

      JU    CA03; 

CA01: L     #i_CFG_Dir_C_Preference; 
      L     0; 
      >I    ; 
      JCN   CA02; 

      L     1; 
      T     #t_DecisionLvl; 

      L     0; 
      L     #i_CFG_Dir_B_Preference; 
      ==I   ; 
      JCN   CA02; 

      L     1; 
      T     #t_FailedReason_B; 
//If no alternative set FailedReason = 1 ("NO ROUTING DEFAULT")

//------------ Dir c -----------------------
CA02: L     1; 
      T     #t_DecisionLvl; 

      L     0; 
      L     #i_CFG_Dir_C_Preference; 
      ==I   ; 
      JCN   CA03; 

      L     1; 
      T     #t_FailedReason_C; 
//If no alternative set FailedReason = 1 ("NO ROUTING DEFAULT")

CA03: L     #i_CFG_Dir_A_Preference; 
      T     #t_Preference_A; 

      L     #i_CFG_Dir_B_Preference; 
      T     #t_Preference_B; 

      L     #i_CFG_Dir_C_Preference; 
      T     #t_Preference_C; 

      L     P##i_ANY_Direction_C; 
      LAR1  ; 

      L     D [AR1,P#6.0]; // Byte6=memory area, Byte7..9=Byte,Bit address
      LAR2  ; 

      L     0; 
      T     DIW [AR2,P#0.0]; 

      L     #t_Preference_A; 
      T     DIB [AR2,P#2.0]; 
      L     #t_Preference_B; 
      T     DIB [AR2,P#3.0]; 
      L     #t_DecisionLvl; 
      T     DIB [AR2,P#5.0]; 
      L     #t_FailedReason_A; 
      T     DIB [AR2,P#6.0]; 
      L     #t_FailedReason_B; 
      T     DIB [AR2,P#7.0]; 

      L     #t_Preference_C; 
      T     DIB [AR2,P#4.0]; 
      L     #t_FailedReason_C; 
      T     DIB [AR2,P#8.0]; 

END_FUNCTION

