FUNCTION "FC_Write_Bool_RecOff" : VOID
TITLE =%version: 0.01 % CN: 31
//Function: 
//Writes a BOOL into a specified position in a specified Record within a DB.
//
//History:
//This version  %created_by: nldy %
//              %date_created: Friday, September 15, 2017 2:16:43 PM %
//              %release: VI_GEN/RA_Dev %
//
//Modification History:                                   By:    Date:     Rev:
//----------------------------------------------------  -------  --------  -----
//Initial version based on v0.10 FC_Write_BOOL_Record   nldy     15-09-17  0.01
//
//Description:
//
//  Return value:
//       0: no error 
//    3122: DB_Number not valid
//    3123: Record_Number not valid
//    3124: DB is too small or record number too big
//    
FAMILY : General
VERSION : 0.0


VAR_INPUT
  i_Record_Number : INT ;	//Record number
  i_Pointer_Offset : INT ;	//[bytes] offset from pointer on i_ANY_Data_Record
  i_P_BOOL_Record : POINTER ;	//Pointer to BOOL in Record
  i_DB : BLOCK_DB ;	//DataBlock of the record where to write to
  i_BOOL : BOOL ;	//BOOL to be stored in the record
  i_DB_Event_Num : INT ;	//Number of DB event
END_VAR
VAR_TEMP
  t_DB_Number : INT ;	//DB number of Record DB lay-out
  t_Record_Length : INT ;	//Length of one record
  t_Offset : INT ;	//Offset from the start of the record to information to write
  t_Event_Data : INT ;	//Temp event data
  t_Ret_Event : INT ;	//Return value for Write Event
END_VAR
BEGIN
NETWORK
TITLE =BA: Check DB number
//If the DB number is not a valid DB number then the FC is ended.
      OPN   #i_DB; 
      L     DBNO; 
      T     #t_DB_Number; 
      L     0; // IF DB_number is not correct
      <=I   ; // Then end this module
      JCN   BA99; 

      L     0; 
      T     #t_Event_Data; 

      CALL "FC_Write_Event" (// Report event "DB Number does not excist" (3122)
           i_Message_ID             := 3122,
           i_Event_value            := #t_DB_Number,
           i_DB_Event_Num           := #i_DB_Event_Num,
           o_Return_Value           := #t_Ret_Event);

      BEU   ; 

BA99: NOP   0; 
NETWORK
TITLE =CA: Check Record Number
//If the record number is not a valid Record number then the FB is ended.
      L     #i_Record_Number; 
      L     0; 
      <I    ; // IF Record Num < 0 THEN..
      JCN   CA99; 

      L     #i_Record_Number; 
      T     #t_Event_Data; 

      CALL "FC_Write_Event" (// Report event "Invalid Record Number" (3123)
           i_Message_ID             := 3123,
           i_Event_value            := #i_Record_Number,
           i_DB_Event_Num           := #i_DB_Event_Num,
           o_Return_Value           := #t_Ret_Event);

      BEU   ; 

CA99: NOP   0; 
NETWORK
TITLE =DA: Calculate 
//Fetch information out of the pointer
      L     P##i_P_BOOL_Record; //Load pointer to Input parameter 
      LAR1  ; //of the type Pointer

      L     W [AR1,P#0.0]; //Load DB number out of the Pointer
      T     #t_DB_Number; //This DB contains only one UDT of a specific record

      OPN   DB [#t_DB_Number]; //Open this DB
      L     DBLG; //and determine the length, which is also the length of the record
      T     #t_Record_Length; 

      L     D [AR1,P#2.0]; //Load the pointer to the BOOL
      T     #t_Offset; //This is the offset from the start of the record     
NETWORK
TITLE =EA: Check if DB is too small or record number too big

      OPN   #i_DB; // Open DB
      L     DBLG; // AND get its length..
      L     DBW    0; // Length of Header
      -D    ; 
      L     #t_Record_Length; 
      /D    ; // Maximum record number
      L     #i_Record_Number; // Actual record number
      <I    ; 
      JCN   EA01; 

      L     #i_Record_Number; 
      T     #t_Event_Data; 

      OPN   #i_DB; 
      L     DBNO; 
      T     #t_DB_Number; 

      CALL "FC_Write_Event" (// Report event "DB is too small" (3124)
           i_Message_ID             := 3124,
           i_Event_value            := #t_DB_Number,
           i_DB_Event_Num           := #i_DB_Event_Num,
           o_Return_Value           := #t_Ret_Event);

      BEU   ; 

EA01: L     #t_Offset; 
      SRD   3; 
      L     #i_Pointer_Offset; 
      +D    ; 
      L     #t_Record_Length; 
      >D    ; 
      JCN   EA99; 

      L     #i_Record_Number; 
      T     #t_Event_Data; 

      OPN   #i_DB; 
      L     DBNO; 
      T     #t_DB_Number; 

      CALL "FC_Write_Event" (// Report event "DB is too small" (3124)
           i_Message_ID             := 3124,
           i_Event_value            := #t_DB_Number,
           i_DB_Event_Num           := #i_DB_Event_Num,
           o_Return_Value           := #t_Ret_Event);

      BEU   ; 

EA99: NOP   0; 
NETWORK
TITLE =FA: Read destination

      L     #i_Record_Number; //Calculate pointer to record number
      L     #t_Record_Length; 
      *D    ; 
      L     DBW    0; // Added offset of Header
      +D    ; // Included the header
      L     #i_Pointer_Offset; // Including requested offset
      +D    ; 
      SLD   3; //Convert byte address to Pointer format
      L     #t_Offset; //offset to Bool in record
      +D    ; 
      LAR1  ; //Pointer is ready

      OPN   #i_DB; 
      A     #i_BOOL; //get BOOL
      =     DBX [AR1,P#0.0]; //and write into the record
END_FUNCTION

