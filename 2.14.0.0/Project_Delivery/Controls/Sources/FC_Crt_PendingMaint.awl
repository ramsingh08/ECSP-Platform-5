FUNCTION "FC_Crt_PendingMaint" : VOID
TITLE =%version: 0.04 % CN: 31
//Function:
//Create Screening Update message for TCTC interface and copy complete message 
//into the FIFO DB.
//
//
//History:
//This version  %created_by:  inajos %
//              %date_created:  Wednesday, August 01, 2018 12:54:27 PM %
//              %release: VI_GEN/RA_Dev %
//
//Modification History:                                   By:    Date:     Rev:
//----------------------------------------------------  -------  --------  -----
//ra#24842: Correction of variable name                  inajos  01-08-18  0.04
//ra#21133: Screening pending Update                     inajos  18-09-17  0.03
//ra#18904 : Process 4/5 removed ,block updated          inajos  13-12-16  0.02
//Initial version based on SIN_T1_CONV_COMPONENT         inajos  28-11-16  0.01
//
//Description:
//The Screening Update message contains a message header and a message body like 
//displayed below.
//Message header:
// -> Event time
// -> Message type
// -> Message identifier
//Message body:
//For each process type (1-5)
// -> HBA_LNK
// -> TRA_LNK
// -> Confirmation
//
FAMILY : General
VERSION : 0.0


VAR_INPUT
  i_ID : DWORD ;	//ID Record HEX AAAA - ZZSS/OO.
  i_FIFO_TCTC_DB_num : INT ;	//Number of DB TCTC FIFO
  i_Message_Type : INT ;	//30 = Pending Request, 31 = Pending Response
  i_Message_ID : DWORD ;	//Message identifier (PID number or LIC number with mmss)
  i_Process_1 : "UDT_Pending_Maint";	//Data structure: Screening Process 1
END_VAR
VAR_TEMP
  t_Base_Address : DWORD ;	//Base address
  t_AR1_Backup : DINT ;	//Save the contents of AR1
  t_Ret_Val_Word : WORD ;	//Return value Word, not used
  t_Ret_Val_Int : INT ;	//Return value Integer, not Used
END_VAR
BEGIN
NETWORK
TITLE =AA: Create message header and copy to DB_PNPN_OutgoingMsgs
//In this network the message header is created and copied into 
//DB_PNPN_OutgoingMsgs.
//
//Message header:
// -> Event time [DATE_AND_TIME]
// -> Message type [INT]
// -> Message identifier [DINT]
      NOP   0; 
// Copy date and time into message header
      CALL "READ_CLK" (
           RET_VAL                  := #t_Ret_Val_Int,
           CDT                      := "DB_PNPN_OutgoingMsgs".PendingMaint_Message.EventTime);

// Copy message type into message header
// Type = 30 - Pending Status Request
// Type = 31 - Pending Status Response
      L     #i_Message_Type; // Pending Status Request Message
      T     "DB_PNPN_OutgoingMsgs".PendingMaint_Message.Message_Type; 

// Copy correct message identifier into message header
      L     #i_Message_ID; 
      T     "DB_PNPN_OutgoingMsgs".PendingMaint_Message.Message_Identifier; 

NETWORK
TITLE =BA: Copy Process 1 to outgoing messages DB

      L     #i_Process_1.HBA_LNK; 
      T     "DB_PNPN_OutgoingMsgs".PendingMaint_Message.Process_1.HBA_LNK; 

      L     #i_Process_1.TRA_LNK;  
      T     "DB_PNPN_OutgoingMsgs".PendingMaint_Message.Process_1.TRA_LNK; 

      L     #i_Process_1.Confirmation; 
      T     "DB_PNPN_OutgoingMsgs".PendingMaint_Message.Process_1.Confirmation; 
NETWORK
TITLE =CA: Put message in buffer
//Copy complete message into FIFO DB.
      CALL "BLKMOV" (
           SRCBLK                   := "DB_PNPN_OutgoingMsgs".Default,
           RET_VAL                  := #t_Ret_Val_Int,
           DSTBLK                   := "DB_PNPN_OutgoingMsgs".Merged_Message);

      CALL "BLKMOV" (
           SRCBLK                   := "DB_PNPN_OutgoingMsgs".PendingMaint_Message,
           RET_VAL                  := #t_Ret_Val_Int,
           DSTBLK                   := "DB_PNPN_OutgoingMsgs".Merged_Message);

      CALL "FC_FIFO" (
           i_FIFO_DB_Num            := #i_FIFO_TCTC_DB_num,
           i_Mode                   := 'I',
           i_ANY_Data               := "DB_PNPN_OutgoingMsgs".Merged_Message,
           i_ID                     := #i_ID,
           o_RET_VAL_FIFO           := #t_Ret_Val_Word);

END_FUNCTION

