FUNCTION_BLOCK "FB_Zone_Safety"
TITLE =%version: 0.04 % CN: 30 
//Function: 
//Detects individual Estop signal.
//
//History:
//This version  %created_by: nlhdl %
//              %date_created:  maandag 7 december 2015 16:01:19 %
//              %release: VI_RAW/RA_Dev %
//
//Modification History:                                   By:    Date:     Rev:
//----------------------------------------------------  -------  --------  -----
//ra#9751: Add source files ECS-P to VI_RAW             nlHdL    14-12-15   0.04
//ra#9751: Add source files ECS-P to VI_RAW             nlHdL    07-12-15   0.04
//7324: ECS-P General from the RD to RA for platform    nlHdL    06-07-15   0.04
//15807: Block consistency update IO symbols, comment   nlHdL    18-09-13   0.04
//CR8095 Comments input CFG changed                     nlple    06-01-12   0.03
//Update according coding convention                    nlRSwi   09-09-10   0.02
//Initial revision                                      insmahi  09-06-10   0.01
//
//Description:
//The following functions are handled by Zone safety :
//- Detect Zone safety signal.
//- Make the status for Zone safety.
//- Monitor Zone safety and generate safety Zone OK signal after the delay .
//- Reset the detailed status after reset.
//
FAMILY : General
VERSION : 0.0


VAR_INPUT
  i_CFG_Delay_Time : INT ;	//CFG: [0.1sec] Time delay generating zone safety OK Signal
  i_CMD_Reset : BOOL ;	//CMD: Reset command
  i_IO_Zone_Safety : BOOL ;	//Zone Safety signal
END_VAR
VAR_OUTPUT
  o_ST_Zone_Safety : BOOL ;	//ST: Safety Zone activated
  o_Zone_Safety_OK : BOOL ;	//TRUE = Zone Safety safe Signal
END_VAR
VAR
  s_CV_delay : INT ;	//Delay counter to monitor Emergency stop signal
  s_delay_done : BOOL ;	//Delay time done
END_VAR
BEGIN
NETWORK
TITLE =AA : Detect the Safety signal and make status

      A     #i_CMD_Reset; // Reset
      R     #o_ST_Zone_Safety; // Reset detailed staus
      AN    #i_IO_Zone_Safety; // When Zone safety 
      S     #o_ST_Zone_Safety; // Set detailed status
NETWORK
TITLE =BA : Counting the delay

      NOP   0; 
// Count the delay for Safety Zone OK

      A     #o_ST_Zone_Safety; //IF Detailed status
      JCN   BA01; 
      L     #i_CFG_Delay_Time; // THEN preset the counter
      T     #s_CV_delay; 
BA01: NOP   0; 
      AN    #o_ST_Zone_Safety; // Countdown counter on time-base
      A     "M_OS_/0.1_sec."; 
      JCN   BA03; 
      L     #s_CV_delay; 
      L     1; 
      -I    ; 
      T     #s_CV_delay; 
      JPZ   BA02; 
      L     0; 
BA02: T     #s_CV_delay; 

BA03: L     #s_CV_delay; 
      L     0; 
      <=I   ; // IF counter value <=0 
      =     #s_delay_done; // Delay Done
NETWORK
TITLE =CA : Safety Zone OK

      AN    #o_ST_Zone_Safety; // Safety zone status
      A     #s_delay_done; // AND delay done
      =     #o_Zone_Safety_OK; // Safety Zone OK
END_FUNCTION_BLOCK

