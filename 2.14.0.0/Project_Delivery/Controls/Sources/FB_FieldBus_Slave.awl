FUNCTION_BLOCK "FB_FieldBus_Slave"
TITLE =%version: 0.04 % CN: 31
//Function:
//This block collects the information of a Fieldbus slave and assembles a 
//status set of this information.
//
//History:
//This version  %created_by: nlhdl %
//              %date_created: Monday, July 09, 2018 9:37:43 AM %
//              %release: VI_GEN/RA_Dev %
//
//Modification History:                                   By:    Date:     Rev:
//----------------------------------------------------  -------  --------  -----
//ina#10600: Supress asi error fieldbus slave           nlHdL    09-07-18   0.04
//ra#26707: LCC with 24V ok check ASI Puc               nlHdL    04-12-17   0.03
//ra#19447: Added QBad for ET200S module                nlHdL    10-01-17   0.02
//ra#9751: Add source files ECS-P to VI_RAW             nlHdL    14-12-15   0.01
//ra#9751: Add source files ECS-P to VI_RAW             nlHdL    07-12-15   0.01
//7324: ECS-P General from the RD to RA for platform    nlHdL    06-07-15   0.01
//Added init block based on 0.03 FB_DP_Slave_Cabinet    nlHdL    18-04-14   0.01
//
//Description:
//This block collects the information of a Fieldbus slave and assembles a 
//status set of this information.
//
FAMILY : General
VERSION : 0.0


VAR_INPUT
  i_CMD_Reset : BOOL ;	//CMD: Reset command
  i_Available_IO : BOOL  := TRUE;	//Signal for indication IO is OK (no Fieldbus or IO error)
  i_FieldB_NotPrsnt : BOOL ;	//Fieldbus module is not present
  i_FieldB_Error : BOOL ;	//Fieldbus module has an error
  i_ASI_NotPrsnt : BOOL ;	//ASI is not Present
  i_ASI_Error : BOOL ;	//ASI has an error
  i_ASI_CH1_24V_OK : BOOL  := TRUE;	//IO: ASI slaves input 24 Volt Channel 1 ok
  i_ASI_CH2_24V_OK : BOOL  := TRUE;	//IO: ASI slaves input 24 Volt Channel 2 ok
  i_ASI_NOT_Diagnosed : BOOL ;	//Delay ASI Diagnose not updated after trailing edge master error/not present
  i_ASI_1_Delta_Error : BOOL ;	//Actual and Configurated ASI slaves are not the same on ASI network 1
  i_ASI_2_Delta_Error : BOOL ;	//Actual and Configurated ASI slaves are not the same on ASI network 2
  i_QBAD_Module_1 : BOOL ;	//Safety Module 1 indicating that one of the channels is pacified (1 = QBad)
  i_QBAD_Module_2 : BOOL ;	//Safety Module 2 indicating that one of the channels is pacified (1 = QBad)
END_VAR
VAR_OUTPUT
  o_ST_FieldB_NotPrsnt : BOOL ;	//ST: Fieldbus Slave not present
  o_ST_FieldB_Error : BOOL ;	//ST: Fieldbus Slave has on error
  o_ST_ASI_NotPrsnt : BOOL ;	//ST: ASI slave is not present
  o_ST_ASI_Err : BOOL ;	//ST: ASI slave has an error
  o_ASI_CH1_24V_AUX_Err : BOOL ;	//ST: ASI slave Channel 1 error 24 Volt Auxilarry
  o_ASI_CH2_24V_AUX_Err : BOOL ;	//ST: ASI slave Channel 2 error 24 Volt Auxilarry
  o_ST_ASI_1_Delta_Error : BOOL ;	//ST: Error ASI slaves are not the same on ASI network 1
  o_ST_ASI_2_Delta_Error : BOOL ;	//ST: Error ASI slaves are not the same on ASI network 2  
  o_IO_Available_OK_CH1 : BOOL ;	//IO checked by the Fieldbus slave and ASI Channel 1 is OK
  o_IO_Available_OK_CH2 : BOOL ;	//IO checked by the Fieldbus slave and ASI Channel 2 is OK
END_VAR
VAR
  s_ER_Slave_Not_Prsnt : BOOL ;	//Error Slave not present
  s_ER_Slave_Error : BOOL ;	//Error Slave error
  s_ER_ASI_1_Delta_Err : BOOL ;	//Error ASI 1 Slave error
  s_ER_ASI_2_Delta_Err : BOOL ;	//Error ASI 2 Slave error  
  s_ER_ASI_Error : BOOL ;	//Error ASI has an error
  s_ER_ASI_NotPrsnt : BOOL ;	//Error ASI is not Present
  s_ER_ASI_CH1_24V_AUX : BOOL ;	//Error ASI slave Channel 1 error 24 Volt Auxilarry
  s_ER_ASI_CH2_24V_AUX : BOOL ;	//Error ASI slave Channel 2 error 24 Volt Auxilarry
END_VAR
VAR_TEMP
  t_Fieldbus_OK : BOOL ;	//Fielbus OK
  t_ASI_OK : BOOL ;	//ASI OK
END_VAR
BEGIN
NETWORK
TITLE =AA: Reset and detect errors
//Manage detailed statuses
      O     #i_CMD_Reset; 
      ON    #i_Available_IO; 
      R     #s_ER_Slave_Not_Prsnt; 
      R     #s_ER_Slave_Error; 
      R     #s_ER_ASI_1_Delta_Err; 
      R     #s_ER_ASI_2_Delta_Err; 
      R     #s_ER_ASI_Error; 
      R     #s_ER_ASI_NotPrsnt; 

      A     #i_FieldB_NotPrsnt; 
      A     #i_Available_IO; 
      S     #s_ER_Slave_Not_Prsnt; 

      A     #i_FieldB_Error; 
      O     #i_QBAD_Module_1; 
      O     #i_QBAD_Module_2; 
      A     #i_Available_IO; 
      S     #s_ER_Slave_Error; 

      A     #i_ASI_1_Delta_Error; 
      A     #i_Available_IO; 
      S     #s_ER_ASI_1_Delta_Err; 

      A     #i_ASI_2_Delta_Error; 
      A     #i_Available_IO; 
      S     #s_ER_ASI_2_Delta_Err; 

      A     #i_Available_IO; 
      AN    #s_ER_Slave_Not_Prsnt; 
      AN    #s_ER_Slave_Error; 
      =     #t_Fieldbus_OK; 

      A     #t_Fieldbus_OK; 
      AN    #i_ASI_NOT_Diagnosed; 
      A     #i_ASI_Error; 
      S     #s_ER_ASI_Error; 

      A     #t_Fieldbus_OK; 
      AN    #i_ASI_NOT_Diagnosed; 
      A     #i_ASI_NotPrsnt; 
      AN    #s_ER_ASI_1_Delta_Err; 
      AN    #s_ER_ASI_2_Delta_Err; 
      S     #s_ER_ASI_NotPrsnt; 

      A     #t_Fieldbus_OK; 
      AN    #s_ER_ASI_Error; 
      AN    #s_ER_ASI_NotPrsnt; 
      =     #t_ASI_OK; 

      A     #t_ASI_OK; 
      AN    #i_ASI_CH1_24V_OK; 
      =     #s_ER_ASI_CH1_24V_AUX; 

      A     #t_ASI_OK; 
      AN    #i_ASI_CH2_24V_OK; 
      =     #s_ER_ASI_CH2_24V_AUX; 
NETWORK
TITLE =BA: Write to output

      A     #s_ER_Slave_Not_Prsnt; 
      =     #o_ST_FieldB_NotPrsnt; 

      A     #s_ER_Slave_Error; 
      =     #o_ST_FieldB_Error; 

      A     #s_ER_ASI_NotPrsnt; 
      =     #o_ST_ASI_NotPrsnt; 

      A     #s_ER_ASI_Error; 
      =     #o_ST_ASI_Err; 

      A     #s_ER_ASI_1_Delta_Err; 
      =     #o_ST_ASI_1_Delta_Error; 

      A     #s_ER_ASI_2_Delta_Err; 
      =     #o_ST_ASI_2_Delta_Error; 

      A     #s_ER_ASI_CH1_24V_AUX; 
      =     #o_ASI_CH1_24V_AUX_Err; 

      A     #s_ER_ASI_CH2_24V_AUX; 
      =     #o_ASI_CH2_24V_AUX_Err; 

      A     #t_ASI_OK; 
      A     #i_ASI_CH1_24V_OK; 
      =     #o_IO_Available_OK_CH1; 

      A     #t_ASI_OK; 
      A     #i_ASI_CH2_24V_OK; 
      =     #o_IO_Available_OK_CH2; 

END_FUNCTION_BLOCK

