FUNCTION_BLOCK "FB_Host_Send_OPS"
TITLE =%version: 0.02 % CN: 40
//Function:
//Send messages via TCP/IP Communication to OPS.
//
//                                                                        
//History:
//This version  %created_by: nlhdl %
//              %date_created: Wednesday, July 05, 2017 1:14:24 PM %
//
//Modification History:                                   By:    Date:     Rev:
//----------------------------------------------------  -------  --------  -----
//ra#21508: i_ID_Connection format as WORD              nlHdL    05-07-17   0.02
//ra#17264: Moved from RAW to GEN                       nlJClo   21-09-16   0.01
//ra#16996: Update the OPZ according MCFV decomposition nlJClo   09-09-16   0.01
//ra#16996: Update the OPZ according MCFV decomposition nlJClo   09-09-16   0.01
//Initial Revision                                      nlHdL    29-04-15   0.01
//
//Description:
//This FB sends data using the Siemens TCON. The FB can only be used
//with a S7 319 PLC. 
//The messages to be sent will be fetched from a FIFO buffer.
//
FAMILY : GEN
VERSION : 0.0


VAR_INPUT
  i_ID_Connection : WORD ;	//ID Connection number
  i_DB_Send_FIFO : BLOCK_DB ;	//Data Structure: DB send FIFO
END_VAR
VAR_OUTPUT
  o_Connection_Error : BOOL ;	//Connection error
END_VAR
VAR
  s_Send_Message : STRUCT 	//Message to be send
   Header : "UDT_Msg_Header";	
   Body : "UDT_OPS_Msg_Body";	
   Footer : "UDT_Msg_Footer";	
  END_STRUCT ;	
  s_Send_Message_End : BOOL ;	//DO NOT MOVE: Pointer to end of 'Send message' to determine STRUCTURE length
  s_FB_TSEND : "TSEND";	//Siemens TSend block
  s_Ret_Val_Send_FIFO : WORD ;	//Return value in WORD format
  s_Status : WORD ;	//The status of the send module
  s_Specified_Msg_Length : INT ;	//Specified message length
  s_Send_Active : BOOL ;	//Send action is active
  s_Appl_Mess_is_Prepared : BOOL ;	//An application message is prepared
  s_Message_Send : BOOL  := TRUE;	//Previous message has been send
  s_ER_No_Connection : BOOL ;	//The TCP/IP connection is not available anymore.
  s_Done : BOOL ;	//Indicates if send module has finished sending
  s_Error_Send : BOOL ;	//Indicates an error with the send module
  s_Error_Monitor : BOOL ;	//Indicates an error with the send module
  s_Busy_Monitor : BOOL ;	//Indicates that the block is busy during monitoring
END_VAR
VAR_TEMP
  t_Send_FIFO_DBNr : INT ;	//DB number of used Send FIFO DB 
END_VAR
BEGIN
NETWORK
TITLE =AA: Calculate send message length
//
//
      L     P##s_Send_Message_End; // Pointer to end of 'Send message'
      L     P##s_Send_Message; // Pointer to begin of 'Send message'
      -D    ; // Length in bits
      SRD   3; // Record length of 'Send message' in bytes
      T     #s_Specified_Msg_Length; // Length in bits

// Record length of 'Send message' in bytes
// Copy Length to s_Specified_Msg_Length  
NETWORK
TITLE =BA: Monitor Send Done status

      A     #s_Send_Active; 
      O     #s_ER_No_Connection; 
      O     #s_Busy_Monitor; 
      JCN   BA99; 

      CALL #s_FB_TSEND (
           REQ                      := FALSE,
           ID                       := #i_ID_Connection,
           LEN                      := #s_Specified_Msg_Length,
           DONE                     := #s_Done,
           BUSY                     := #s_Busy_Monitor,
           ERROR                    := #s_Error_Monitor,
           STATUS                   := #s_Status,
           DATA                     := #s_Send_Message);

      A     #s_Done; // IF the previous message was send..
      AN    #s_Error_Monitor; // Without an error
      O(    ; 
      AN    #s_Done; 
      AN    #s_Busy_Monitor; 
      AN    #s_Error_Monitor; 
      )     ; 
      A     #s_Send_Active; 
      R     #s_Send_Active; // THEN send action is finished
      R     #s_ER_No_Connection; 

BA99: NOP   0; 
NETWORK
TITLE =CA: Check FIFO for new messages

      OPN   #i_DB_Send_FIFO; //Determine send FIFO's DB nr
      L     DBNO; 
      T     #t_Send_FIFO_DBNr; 

      AN    #s_Send_Active; // AND no message is being send
      AN    #s_ER_No_Connection; 
      AN    #s_Busy_Monitor; 
      AN    DBX    0.0; // AND Send FIFO not empty (empty bit is first bit in FIFO DB)
      =     #s_Appl_Mess_is_Prepared; // THEN an application message is prepared
      JCN   CA99; 

      SET   ; 
      S     #s_Send_Active; 
      R     #s_Message_Send; 

      CALL "FC_FIFO" (// THEN retrieve one message from Send FIFO
           i_FIFO_DB_Num            := #t_Send_FIFO_DBNr,
           i_Mode                   := 'O',
           i_ANY_Data               := #s_Send_Message.Body,
           i_ID                     := "MD_DWORD_To_Do",
           o_RET_VAL_FIFO           := #s_Ret_Val_Send_FIFO);


CA99: NOP   0; 
NETWORK
TITLE =DA: Send Message 

      A     #s_Error_Send; 
      AN    #s_Message_Send; 
      O     #s_Appl_Mess_is_Prepared; 
      JCN   DA99; 

      CALL #s_FB_TSEND (
           REQ                      := TRUE,
           ID                       := #i_ID_Connection,
           LEN                      := #s_Specified_Msg_Length,
           DONE                     := #s_Done,
           BUSY                     := #s_Busy_Monitor,
           ERROR                    := #s_Error_Send,
           STATUS                   := #s_Status,
           DATA                     := #s_Send_Message);

      A     #s_Done; 
      A     #s_Send_Active; 
      AN    #s_Error_Send; 
      S     #s_Message_Send; 
      R     #s_Send_Active; 
      R     #s_ER_No_Connection; 

// Detect and set No_connection_error
      L     #s_Status; 
      L     W#16#80A1; // Communications Error
      ==I   ; 
      O(    ; 
      L     #s_Status; 
      L     W#16#80C4; // Temporary Communications Error
      ==I   ; 
      )     ; 
      =     #s_ER_No_Connection; // Set No connection error

DA99: NOP   0; 
NETWORK
TITLE =EA: Report error Send block

      A     #s_ER_No_Connection; 
      =     #o_Connection_Error; 
END_FUNCTION_BLOCK

