FUNCTION_BLOCK "FB_EQP_Lean"
TITLE =%version: 1.11 % CN: 50 
//Function:
//Module for Lean conveyor control
//
//History:
//This version  %created_by: nlhdl %
//              %date_created: Tuesday, November 14, 2017 2:22:25 PM %
//              %release: VI_GEN/RA_Dev %
//
//Modification History:                                   By:    Date:     Rev:
//----------------------------------------------------  -------  --------  -----
//ra#26353: cascade start-up in Lean                    nlHdL    14-11-17   1.11
//ina#1918: flow control configuration                  nlJClo   24-01-17   1.10
//ra#18816: Clear PLT layer EQP equipment               nlHdL    02-12-16   1.09
//ra#13986: Add the External Stop status to EQP         nlHdL    02-06-16   1.08
//ra#9751: Add source files ECS-P to VI_RAW             nlHdL    14-12-15   1.07
//ra#9751: Add source files ECS-P to VI_RAW             nlHdL    07-12-15   1.07
//7324: ECS-P General from the RD to RA for platform    nlHdL    06-07-15   1.07
//23445: wrongfully remove last entry during pull fase  nlHdL    07-04-15   1.07
//23247: Trailing edge in Runout section no IHO bit     nlHdL    13-03-15   1.06
//15807: Block consistency update IO symbols, comment   nlHdL    18-09-13   1.05
//PR12648 Remove modification wrong interpretation PR   nlple    04-09-13   1.04
//PR12648 Cascade start up added                        nlple    03-09-13   1.03
//14506: Add Motor_Stopped to EQP Basic & Lean          nlHdL    02-09-13   1.02
//14196: Set init value section_run_downstream          nlHdL    11-06-13   1.01
//13971: Modified PLT concept with state Version1       nlHdL    29-05-13   1.00
// Init version based on version 0.27 of PLT version 0
//
//Description:
//
//!!This block is in LAD!!
//
//The transport function that can be executed by this module, are:
//- Displacement
//- PLT
//- Handover
//- External
//
//Explanation of the parameters:
//  Parameter prefixes:
//    HW: field IO
//    SET: setting
//    CFG: configuration
//    CMD: command
//    ST: Status
//
FAMILY : General
VERSION : 0.0


VAR_INPUT
  i_ID_Section : DWORD ;	//ID of Section ID Record HEX AAAA - ZZSS.
  i_Basic_Offset : INT ;	//[cm] Distance between begin of conveyor and next handling point (mostly a PEC)
  i_DB_PLT : BLOCK_DB ;	//Data structure: Position datablock
  i_DB_PLT_DwnStr : BLOCK_DB ;	//Data Structure: Position DataBlock Downstream section
  i_DB_Event : BLOCK_DB ;	//Data Structure: Event DB
  i_CFG_Conveyor_Speed : INT ;	//CFG: [mm/sec] Conveyor Speed
  i_CFG_Run_Out_Distance : INT  := 14;	//CFG: [cm] Run out distance from nominal speed
  i_CFG_Start_Up_Distance : INT  := 7;	//CFG: [cm] Distance from standstill to nominal speed.
  i_PPI_Displacement : INT ;	//The displacement coming from the PPI in [mm]
  i_Dwnstr_PLT_Pos : INT ;	//CFG: Total offset from downstream datahandling point in [cm] (previous pos)
  i_CFG_Del_Window_Codes : BOOL ;	//CFG: Empty window and window reservation codes are deleted at the end
  i_Available_IO : BOOL  := TRUE;	//Signal for indication IO is OK (no Fieldbus or IO error)
  i_Safety_OK : BOOL  := TRUE;	//Safety is OK signal
  i_CMD_Enable : BOOL ;	//CMD: Enable command
  i_CMD_Start : BOOL ;	//CMD: Start command
  i_Ext_Req_Run : BOOL ;	//Request signal from external to run 
  i_Ext_Req_Stop : BOOL ;	//Request signal from external to stop (restart needed)
  i_Ext_Req_Halt : BOOL ;	//Request signal from external to halt (no restart needed)
  i_Ext_Req_Dis_Send : BOOL ;	//Request signal from external disable send products
  i_OS_Direction_Change : BOOL ;	//Reset handover on One-Shot direction change
  i_Clr_PLT_Data : BOOL ;	//Clear PLT Data when LMS is not automatic
END_VAR
VAR_OUTPUT
  o_External : "UDT_External_Outputs";	//Data Structure: External record
  o_Motor_Run : BOOL ;	//Command run motor to HW layer
  o_ST_Items : BOOL ;	//ST: Items present on section
  o_ST_Running : BOOL ;	//ST: Section is running
  o_ST_Started : BOOL ;	//ST: Started
  o_ST_Request_Halt : BOOL ;	//ST: Request Halt
  o_ST_Request_Stop : BOOL ;	//ST: Request Stop
  o_ST_External_Stop : BOOL ;	//ST: Component is operationally off and not available for operational use
  o_Ready_To_Restart : BOOL ;	//Ready To Restart to system control
END_VAR
VAR
  s_FB_Displacement : "FB_Displacement";	//Multi Instance block call
  s_FB_Manage_PLT : "FB_Manage_PLT";	//Multi Instance block call
  s_FB_HandOver : "FB_Handover";	//Multi Instance block call
  s_DB_Num_PLT : INT ;	//Number of the PLT DB
  s_DB_Num_Event : INT ;	//Number of the Event DB
  s_DB_Num_PLT_Downstream : INT ;	//Number ot hte PLT DB downstream section
  s_Length_Section : INT ;	//[cm] Length of this section.
  s_Displacement : INT ;	//[cm] Displacement of the section
  s_Act_Run_Out_Distance : INT ;	//Actual calculated run-out distance
  s_Section_Run : BOOL ;	//PLT: Indication if the section is running
  s_Motor_On_Speed : BOOL ;	//Motor on nominal speed
  s_Motor_Stopped : BOOL ;	//Motor is stopped (speed = 0)
  s_Eqp_Operational_On : BOOL ;	//Operational on signal of this section
  s_Stop_Motor : BOOL ;	//Request to stop the motor
  s_Handover_Run_OK : BOOL ;	//Indicating that for the handover function its ok to run the section
  s_Item_In_Handover_Area : BOOL ;	//Indicating an item is in the handover area
  s_Section_Run_Downstream : BOOL  := TRUE;	//Section Run Signal Downstream
  s_Section_Available : BOOL ;	//Section Available Upstream
  s_FP_Handover_Active : BOOL ;	//Positive flank detection when handover is active
  s_Disable_Send : BOOL ;	//Request to stop the motor
  s_Inhibit_Rcv_Item_Dwn : BOOL ;	//Downstream inhibit to receive item
  s_FN_Clear_PLT : BOOL ;	//Negative flank of Clear PLT data
END_VAR
VAR_TEMP
  t_Not_Used_Int : INT ;	//Not used integer
  t_DB_PLT_Num_Downstream : INT ;	//Number of the downstream PLT DB
  t_number_items_found : INT ;	//Number of items found in search
  t_ready_to_restart : BOOL ;	//Equipment is ready to restart
  t_Disable_Item : BOOL ;	//Item is disabled
  t_Leading_Edge_Found : BOOL ;	//Leading edge found in PLT
  t_Trailing_Edge_Found : BOOL ;	//Trailing edge found in PLT
  t_Pull_Dwn_Stopped : BOOL ;	//Pull fase downstream stopped
  t_Clear_PLT : BOOL ;	//Clear PLT command
  t_PEC_Latch_PLT : BOOL ;	//PEC Latch for PLT
END_VAR
BEGIN
NETWORK
TITLE =AA: Read PLT DB num
//Read all required parameters from the PLT DB
      OPN   #i_DB_Event; 
      L     DBNO; 
      T     #s_DB_Num_Event; 

      OPN   #i_DB_PLT; // Open tracking DB
      L     DBNO; 
      T     #s_DB_Num_PLT; // Determine PLT block DB number

      A     DBX   10.1; 
      =     #s_Section_Run; 

      A     DBX   10.7; 
      =     #t_PEC_Latch_PLT; 

      L     DBW   12; 
      T     #s_Length_Section; 

      A     #i_CMD_Start; 
      JCN   AA01; 

//Check the run out on start command
      L     #i_CFG_Run_Out_Distance; 
      L     #s_Length_Section; 
      >I    ; 
      JCN   AA01; 

//Report that run out is to large
      CALL "FC_Write_Event" (
           i_Message_ID             := 3005,
           i_Event_value            := #i_CFG_Run_Out_Distance,
           i_DB_Event_Num           := #s_DB_Num_Event,
           o_Return_Value           := #t_Not_Used_Int);

AA01: L     DBW    8; // Read downstream PLT DB number from PLT DB.
      T     #t_DB_PLT_Num_Downstream; // The input i_DB_PLT_DwnStr cannot be used because the DB can be swapped (in reverse mode)

      OPN   #i_DB_PLT_DwnStr; // Open downstream PLT DB block
      L     DBNO; 
      T     #s_DB_Num_PLT_Downstream; // Determine PLT block DB number for FB_Manage_PLT (needed only in the first scan or if done bit in PLT is FALSE)

      L     #t_DB_PLT_Num_Downstream; // Check if downstream PLT DB is connected
      L     1; 
      >I    ; 
      JCN   AA99; 

      OPN   DB [#t_DB_PLT_Num_Downstream]; 

      A     DBX   10.1; 
      =     #s_Section_Run_Downstream; 

      A     DBX   11.0; 
      =     #s_Inhibit_Rcv_Item_Dwn; 

AA99: NOP   0; 
NETWORK
TITLE =BA: Call smart blocks
// 
      CALL #s_FB_Displacement (
           i_CFG_Conveyor_Speed     := #i_CFG_Conveyor_Speed,
           i_CFG_Run_Out_Distance   := #i_CFG_Run_Out_Distance,
           i_CFG_Start_Up_Distance  := #i_CFG_Start_Up_Distance,
           i_PPI_Displacement       := #i_PPI_Displacement,
           i_Section_Run            := #s_Section_Run,
           o_Motor_On_Speed         := #s_Motor_On_Speed,
           o_Motor_Stopped          := #s_Motor_Stopped,
           o_Displacement           := #s_Displacement,
           o_Act_Run_Out_Distance   := #s_Act_Run_Out_Distance,
           o_Act_Start_Up_Distance  := #t_Not_Used_Int);

      CALL #s_FB_Manage_PLT (
           i_ID                     := #i_ID_Section,
           i_DB_PLT_Num             := #s_DB_Num_PLT,
           i_DB_PLT_Num_DwnStr      := #s_DB_Num_PLT_Downstream,
           i_DB_Event_Num           := #s_DB_Num_Event,
           i_Basic_Offset           := #i_Basic_Offset,
           i_Dwnstr_PLT_Pos         := #i_Dwnstr_PLT_Pos,
           i_Displacement           := #s_Displacement,
           i_CFG_Del_Window_Codes   := #i_CFG_Del_Window_Codes,
           i_Section_Run            := #s_Section_Run,
           o_Pull_Dwn_Stopped       := #t_Pull_Dwn_Stopped);

      CALL #s_FB_HandOver (
           i_DB_PLT_Num             := #s_DB_Num_PLT,
           i_DB_Event_Num           := #s_DB_Num_Event,
           i_Position_HO            := #s_Act_Run_Out_Distance,
           i_PEC_Offset             := #i_Dwnstr_PLT_Pos,
           i_Section_Run_Downstream := #s_Section_Run_Downstream,
           i_OS_Direction_Change    := #i_OS_Direction_Change,
           o_Handover_Run_OK        := #s_Handover_Run_OK,
           o_Item_In_HandOver_Area  := #s_Item_In_Handover_Area);

NETWORK
TITLE =CA: Determine 'Operational_ON' status
//LAD!
      A     #i_CMD_Start; 
      S     #s_Eqp_Operational_On; 
      A(    ; 
      ON    #i_CMD_Enable; 
      ON    #i_Available_IO; 
      O     #i_Ext_Req_Stop; 
      ON    #i_Safety_OK; 
      )     ; 
      R     #s_Eqp_Operational_On; 
      A     #s_Eqp_Operational_On; 
      =     #o_External.Operational_On; 
NETWORK
TITLE =DA: Stop Motor
//LAD!
      ON    #s_Eqp_Operational_On; 
      ON    #i_Safety_OK; 
      O     #i_Ext_Req_Stop; 
      =     #s_Stop_Motor; 
NETWORK
TITLE =EA: Clear PLT

      A     #i_Clr_PLT_Data; 
      AN    #t_PEC_Latch_PLT; 
      FN    #s_FN_Clear_PLT; 
      =     #t_Clear_PLT; 

      A     #t_Clear_PLT; 
      JCN   EA01; 

//Clear PLT 
      CALL "FC_PLT_Clr_DB" (
           i_Enable_Clr             := #t_Clear_PLT,
           i_DB_PLT_Num             := #s_DB_Num_PLT,
           i_DB_Event_Num           := #s_DB_Num_Event);

EA01: NOP   0; 
NETWORK
TITLE =FA: Halt Disable Send
//LAD!
      A(    ; 
      A     #s_Item_In_Handover_Area; 
      FP    #s_FP_Handover_Active; 
      O     ; 
      A     #s_Disable_Send; 
      AN    #t_Clear_PLT; 
      )     ; 

      A     #i_Ext_Req_Dis_Send; 
      =     #s_Disable_Send; 
NETWORK
TITLE =GA: Downstream is inhibit to Receive item

      A     #s_Item_In_Handover_Area; 
      A     #s_Inhibit_Rcv_Item_Dwn; 
      O     #s_Disable_Send; 
      =     #t_Disable_Item; 
NETWORK
TITLE =HA: Motor Control
//LAD!
      A     #i_Safety_OK; 
      AN    #s_Stop_Motor; 
      A     #i_Ext_Req_Run; 
      A     #s_Handover_Run_OK; 
      AN    #i_Ext_Req_Halt; 
      AN    #t_Disable_Item; 
      =     #o_Motor_Run; 
      =     #s_Section_Run; 
      =     #o_External.Motor_Run; 
NETWORK
TITLE =IA: Status Items Present

      CALL "FC_PLT_Srch_Inf_Dwn" (
           i_DB_PLT_Num             := #s_DB_Num_PLT,
           i_Position               := #s_Length_Section,
           i_Window                 := #s_Length_Section,
           i_Info                   := 1,
           o_Position               := #t_Not_Used_Int,
           o_Info                   := #t_Not_Used_Int,
           o_LNK                    := #t_Not_Used_Int,
           o_DB_PLT_Num             := #t_Not_Used_Int,
           o_Number_Items_Found     := #t_number_items_found);

      L     #t_number_items_found; 
      L     0; 
      >I    ; 
      =     #t_Leading_Edge_Found; 

      CALL "FC_PLT_Srch_Inf_Dwn" (
           i_DB_PLT_Num             := #s_DB_Num_PLT,
           i_Position               := #s_Length_Section,
           i_Window                 := #s_Length_Section,
           i_Info                   := 2,
           o_Position               := #t_Not_Used_Int,
           o_Info                   := #t_Not_Used_Int,
           o_LNK                    := #t_Not_Used_Int,
           o_DB_PLT_Num             := #t_Not_Used_Int,
           o_Number_Items_Found     := #t_number_items_found);

      L     #t_number_items_found; 
      L     0; 
      >I    ; 
      =     #t_Trailing_Edge_Found; 

      A     #t_Leading_Edge_Found; 
      O     #t_Trailing_Edge_Found; 
      O     #s_Item_In_Handover_Area; 
      =     #o_ST_Items; 
NETWORK
TITLE =JA: Status running
//LAD!
      A     #s_Section_Run; 
      =     #o_ST_Running; 
NETWORK
TITLE =KA: Status Started
//LAD!
      A     #s_Eqp_Operational_On; 
      AN    #s_Stop_Motor; 
      =     #o_ST_Started; 
NETWORK
TITLE =LA: Status Request Halt
//LAD!
      A     #i_Ext_Req_Halt; 
      =     #o_ST_Request_Halt; 
NETWORK
TITLE =MA: Status Request Stop
//LAD!
      A     #i_Ext_Req_Stop; 
      =     #o_ST_Request_Stop; 
NETWORK
TITLE =NA: External stop
//LAD!
      ON    #i_Available_IO; 
      ON    #i_Safety_OK; 
      =     #o_ST_External_Stop; 
NETWORK
TITLE =OA: Ready to restart indication to Equipment Control
//LAD!
      AN    #s_Eqp_Operational_On; 
      A     #i_CMD_Enable; 
      AN    #i_Ext_Req_Stop; 
      A     #i_Safety_OK; 
      A     #i_Available_IO; 
      =     #o_Ready_To_Restart; 
NETWORK
TITLE =PA: Section Available
//LAD!
      A     #o_ST_Started; 
      AN    #o_ST_Request_Halt; 
      AN    #o_ST_Request_Stop; 
      =     #s_Section_Available; 
NETWORK
TITLE =QA: Write outputs

      OPN   #i_DB_PLT; 

      A     #s_Section_Run; // Write section run signal
      =     DBX   10.1; 

      A     #s_Motor_On_Speed; // Write on speed signal
      =     DBX   10.2; 
      =     #o_External.Motor_On_Speed; 

      A     #s_Section_Available; // Write section available to recieve
      =     DBX   10.4; 

      A     #o_ST_Started; // Write section operational
      =     DBX   10.5; 
      =     DBX   10.3; //Cascade Bit

      A     #s_Item_In_Handover_Area; //Write Item is at Handover
      =     DBX   10.6; 

      A     #s_Motor_Stopped; //Write Motor Stopped
      =     DBX   11.1; 
END_FUNCTION_BLOCK

