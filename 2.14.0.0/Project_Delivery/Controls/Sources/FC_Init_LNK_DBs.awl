FUNCTION "FC_Init_LNK_DBs" : VOID
TITLE =%version: 0.04 % CN: 31 
//Function:
//Write the default DB number into the Header, and the entries present.
//
//History:                                                                        
//This version   %created_by: nlhdl %
//              %date_created: maandag 7 december 2015 16:05:01 %
//              %release: VI_RAW/RA_Dev %
//
//Modification History:                                   By:    Date:     Rev:
//----------------------------------------------------  -------  --------  -----
//ra#9751: Add source files ECS-P to VI_RAW             nlHdL    14-12-15   0.04
//ra#9751: Add source files ECS-P to VI_RAW             nlHdL    07-12-15   0.04
//7990: Cart Bagtrax Record added to library            nlHdL    01-10-15   0.04
//7324: ECS-P General from the RD to RA for platform    nlHdL    06-07-15   0.03
//15807: Block consistency update IO symbols, comment   nlHdL    18-09-13   0.03
//Remove author and name fields                         nlMRe    22-09-10   0.02
//Initial revision                                      nlHdL    09-09-10   0.01
//
//Description:
//Calculated the length of the UDT and write this length into 
//the global DB. Write the default DB number into the Header, and the entries 
//present.
//
FAMILY : General
VERSION : 0.0


VAR_INPUT
  i_Position_UDT_LNK : INT ;	//Position in the LNK List DB of the add UDT list
  i_DB_List : BLOCK_DB ;	//Block DB of List DB, selection of which TYPE of Link (LIC, TIC, ect)
END_VAR
VAR_TEMP
  t_DB_Number : INT ;	//DB number of the block
  t_Chk_Position : INT ;	//Check the input position
END_VAR
BEGIN
NETWORK
TITLE =AA: Check current length value

      OPN   "DB_LNK_List"; 
      A     DBX   10.0; // IF Done bit is set end block
      AN    "M_First_Scan"; 
      BEC   ; 

      L     #i_Position_UDT_LNK; 
      L     0; 
      ==I   ; 
      BEC   ; // End block if position is zero

      OPN   "DB_LNK_List"; 
      L     DBW    0; 
      L     12; // Start  check from DB_LNK_List DBW12
      -I    ; 
      T     #t_Chk_Position; // Length of UDT_LNK_Record for position check

      L     #i_Position_UDT_LNK; 
      L     2; 
      *I    ; 
      L     #t_Chk_Position; 
      <I    ; 
      JC    AA99; // IF request position smaller than length of UDT then continu 

      BE    ; // Else end block

AA99: NOP   0; 
NETWORK
TITLE =BA: Calculated the record length

      OPN   #i_DB_List; 
      L     DBNO; 
      T     #t_DB_Number; 

      OPN   "DB_LNK_List"; //Open the DB_LNK_List
      L     #i_Position_UDT_LNK; 
      L     1; 
      -I    ; 
      L     2; 
      *I    ; 
      L     12; 
      +I    ; 
      SLW   3; 
      LAR1  ; // Pointer to LNK entry in DB_LNK

      L     #t_DB_Number; 
      T     DBW [AR1,P#0.0]; //Copy the i_DB_List DB number to the DB_LNK_List
END_FUNCTION

