FUNCTION_BLOCK "FB_OPS_LifeSign"
TITLE =%version: 0.02 % CN: 33
//Function:
//Handle lifesign messages.
//
//
//History:
//This version  %created_by:  nljclo %
//              %date_created:  Wednesday, September 21, 2016 2:22:06 PM %
//              %release: VI_GEN/RA_Dev %
//
//Modification History:                                   By:    Date:     Rev:
//----------------------------------------------------  -------  --------  -----
//ra#17264: Moved from RAW to GEN                       nlJClo   21-09-16   0.02
//ra#16996: Update the OPZ according MCFV decomposition nlJClo   09-09-16   0.02
//ra#16996: Update the OPZ according MCFV decomposition nlJClo   09-09-16   0.02
//24499: FIFO handling done incorrect for OPS/Lifesign  nlHdL    09-07-15   0.02
//Initial Revision                                      nlHdL    29-04-15   0.01
//
//Description:
//This block handles the lifesign messages to check if the message 
//communication is working. Input "i_CFG_Send_Lifesign_Req" can be used to 
//configure if this side of the OPS station will be used as server (send 
//lifesign requests) or as client (respond to lifesign requests with a lifesign 
//response).
//
FAMILY : GEN
VERSION : 0.0


VAR_INPUT
  i_ID : DWORD ;	//ID Record HEX AAAA - ZZSS/OO.
  i_DB_Send_OPS_FIFO : BLOCK_DB ;	//Data Structure: DB send FIFO
  i_DB_Recv_OPS_FIFO : BLOCK_DB ;	//Data Structure: TCTC DB receive FIFO
  i_CFG_Lifesign_Interval : INT ;	//CFG: [sec] Interval for sending a LifeSignRequest msg (0 = send no LifeSigns)
  i_CFG_NumLifeSgnMiss_Err : INT  := 1000;	//CFG: [Num] Number of Lifesign responses to be missed before an error is set
  i_Event_DB : BLOCK_DB ;	//Data Structure: Event DataBlock 	
END_VAR
VAR_OUTPUT
  o_LifeSign_Err : BOOL ;	//Report LifeSign Error to the parent
END_VAR
VAR
  s_Msg_OPS_LifeSign : "UDT_OPS_Msg_Lifesign";	//Content of lifesign response message that was received
  s_Last_Sent_Seq_Num : INT ;	//Sequence/signature number of the last Life Sign Request that was sent
  s_Timer_LifeSign_Reply : INT ;	//Timer to evaluate if the lifesign Reply signal was received on time
  s_Timer_LifeSignInterval : INT ;	//Current value of the timer that is used to send the LifeSignRequest
  s_Cnt_LifeSigns_Missed : INT ;	//Number of consecutive lifesign Responses not received from LM
  s_Wait_For_Reply : BOOL ;	//The LifeSign Request was send and timer "wait for reply" is running
  s_Done : BOOL ;	//Bit to indicate that pointer calculation has been done
  s_Communication_Error : BOOL ;	//No LifeSign received from LM
END_VAR
VAR_TEMP
  t_DB_Event_Num : INT ;	//DB number of the event DB
  t_DB_Send_OPS_FIFO : INT ;	//Number of the Send DB FIFO that contain the LifeSign Signals	
  t_DB_Recv_OPS_FIFO : INT ;	//Number of the Receive DB FIFO that contain the LifeSign Signals	
  t_Message_Received : BOOL ;	//New Message Received
  t_OS_LifeSign_Reply_Mis : BOOL ;	//One shot - time to receive the lifesign elapsed
  t_OS_LifeSign_Interval : BOOL ;	//One shot - send life sign request/life sign shall be received
  t_Incorrect_Reply_MSG : BOOL ;	// The received LifeSignResponse contain incorrect signature
END_VAR
BEGIN
NETWORK
TITLE =AA: Init
//Copy DB numbers to temporary, get base address and initialize timers.
      OPN   #i_Event_DB; 
      L     DBNO; 
      T     #t_DB_Event_Num; 

      OPN   #i_DB_Send_OPS_FIFO; 
      L     DBNO; 
      T     #t_DB_Send_OPS_FIFO; 

      OPN   #i_DB_Recv_OPS_FIFO; 
      L     DBNO; 
      T     #t_DB_Recv_OPS_FIFO; 

      O     "M_First_Scan"; // The pointer calculation needs to be called only once. 
      ON    #s_Done; // and in case a new instance is downloaded.
      JCN   AA99; 

// As a Initial value the communication error shall be set
      SET   ; // Initially set LifeSign error
      =     #s_Communication_Error; 

// Initialize Timers
      L     0; 
      T     #s_Timer_LifeSignInterval; 
      T     #s_Timer_LifeSign_Reply; 

// Set bit to indicate that this network has been called
      SET   ; 
      =     #s_Done; 

AA99: NOP   0; 
NETWORK
TITLE =BA: Check if the lifesign check procedure shall be executed
//The lifesign check procedure shall only be executed when the 
//i_CFG_Lifesign_Interval is not configured to be 0 and handover data is 
//send message based via the PN/PN coupler.
      NOP   0; 
// Check if the LifeSign signal shall be evaluated
      L     #i_CFG_Lifesign_Interval; 
      L     0; 
      ==I   ; 
      JCN   BA01; // Don't send a lifesign request message if interval = 0

// Reset the error status output if the error status must be suppressed.
      R     #o_LifeSign_Err; 
      BEU   ; 

// Initialize Temp Variables
BA01: CLR   ; 
      =     #t_Message_Received; 
      =     #t_OS_LifeSign_Interval; 
      =     #t_OS_LifeSign_Reply_Mis; 
NETWORK
TITLE =CA: Retrieve the lifesign reply message
//All the LifeSign messages for one connection are stored within one DB FIFO
//(DB_Rcv_LifeSign_FIFO). When a new message is detected in the FIFO DB the 
//message type within the message has to be checked at first. Only when the 
//retrieved message is a lifesign request (200) or a lifesign response (201) it 
//shall be retrieved from the FIFO DB.
      NOP   0; 
// Check if new message is present
      OPN   DB [#t_DB_Recv_OPS_FIFO]; 

      L     DBW    6; // When the DB_FIFO is empty the record "Num_Of_Records_stored" within the DB is equal to "0"
      L     0; 
      ==I   ; 
      JC    CA99; // IF DB_FIFO is empty then end this Network

// Create pointer to the message type within the first available message
// The first available message within the FIFO DB is specified via the entry "Next_Record_Retrieve"
// Calculate the Offset to the first available message
      L     0; 
      LAR1  ; 

      L     DBW   12; // Position of the record
      L     1; 
      -I    ; 
      L     DBW    4; // The length of each message in the DB_FIFO ("Record_Data_Length")
      *I    ; 
      +     14; // Length of the general record in DB_FIFO/Offset to the first message in the DB_FIFO
      SLD   3; 
      LAR1  ; // Store the Offset, to the first available message, in the Accumulator 1.

// Retrieve the message type from the message to check if it is a lifesign request or lifesign response (200 or 201)
      A(    ; // IF the retrieved message is a Lifesign Request
      L     DBW [AR1,P#8.0]; // Retrieve the message type from the first available message from DB_FIFO
      L     201; // Compare the retrieved message type with 200.
      ==D   ; 
      )     ; 
      JCN   CA99; // If the message is a lifesign request, then retrieve the record

      L     DBW [AR1,P#8.0]; 
      T     #s_Msg_OPS_LifeSign.Header.Message_Type; //Life Sign Response message
      L     DBW [AR1,P#18.0]; 
      T     #s_Msg_OPS_LifeSign.Sequence_Number; //has the correct sequence number

// Update FiFo header entry is used as LifesignResponse
      L     DBW    6; 
      L     1; 
      -I    ; 
      T     DBW    6; 

      L     DBW   12; 
      L     1; 
      +I    ; 
      T     DBW   12; 
      L     DBW    8; 
      >I    ; 
      JCN   CA01; 

      L     1; 
      T     DBW   12; 

CA01: SET   ; 
      =     #t_Message_Received; 
      L     DBW    6; 
      L     0; 
      ==I   ; 
      S     DBX    0.0; 

CA99: NOP   0; 
NETWORK
TITLE =DA: Check block mode and update timers
//The timers are run to be able to send a lifesign request message every x 
//seconds or evaluate if the new lifesign request shall be received (depending on 
//the configuration) as well as the evaluation if the lifesign reply was received 
//in time.
      NOP   0; 
// Update Timers
// The LifeSign signal/request and the LifeSign response shall send/receive within the pre-configured time
// therefore the following timers are introduced.

// LifeSignInterval timer 
// The "Timer_LifeSignInterval" specifies the time, when the lifesign request shall be
// send/received (depending on the mode)
      L     #s_Timer_LifeSignInterval; 
      L     "MW_Prev_Cycle_Time"; 
      +D    ; 
      T     #s_Timer_LifeSignInterval; 

// LifeSign_Reply timer
// The Timer_LifeSign_reply in necessary of the evaluation, if the LifeSign reply 
// was received within the configured time and it is used only in the Server Mode
      A     #s_Wait_For_Reply; 
      JCN   DA99; 

      L     #s_Timer_LifeSign_Reply; 
      L     "MW_Prev_Cycle_Time"; 
      +D    ; 
      T     #s_Timer_LifeSign_Reply; 

DA99: NOP   0; 
NETWORK
TITLE =EA: Evaluate timer statuses

      NOP   0; 
// Evaluate lifesign interval timer
      L     #i_CFG_Lifesign_Interval; 
      L     1000; // Multiply with 1000 to convert to milli seconds
      *D    ; 
      L     #s_Timer_LifeSignInterval; 
      <=D   ; 
      S     #t_OS_LifeSign_Interval; 

      L     #i_CFG_Lifesign_Interval; 
      L     1000; // Multiply with 1000 to convert to milli seconds
      *D    ; 
      L     #s_Timer_LifeSignInterval; 
      <=D   ; 
      S     #t_OS_LifeSign_Interval; 


// Reset LifeSignInterval Time
      A     #t_OS_LifeSign_Interval; 
      JCN   EA03; 

      L     0; 
      T     #s_Timer_LifeSignInterval; 

// Evaluate LifeSign_reply Timer
EA03: L     #i_CFG_Lifesign_Interval; 
      L     1000; // Multiply with 1000 to convert to milli seconds
      *D    ; 
      L     #s_Timer_LifeSign_Reply; 
      <=D   ; 
      S     #t_OS_LifeSign_Reply_Mis; 
      JCN   EA99; 

      L     0; 
      T     #s_Timer_LifeSign_Reply; 

      L     #s_Last_Sent_Seq_Num; // To make sure that the reply that was received to late will not be counted as a correct reply message
      +I    ; 
      T     #s_Last_Sent_Seq_Num; 

      CLR   ; 
      =     #s_Wait_For_Reply; 
EA99: NOP   0; 
NETWORK
TITLE =FA: Handle the received message and send lifesign response
//This block call can works as a server (Life Sign Request generator) and in this 
//case the received LifeSign message shall has Message_Type_ID equal to "201"
//
//If the received message is a Life Sign Response to the Life Sign Request that 
//was last sent for this area and the Life Sign was received before the maximum 
//response time is elapsed:
//(1) Set the Life Sign Response received bit. 
//(2) Reset the No Communication error.
//(3) Reset the LifeSigns-missed counter.
//(4) Reset the LifeSign reply timer
//
//The second mode supported by that block is a client (Life Sign Response 
//generator). and in that case the received LifeSign message shall have 
//Message_Type_ID equal to "200"
//
//If the received message is a Life Sign Request the Life Sign Response shall be 
//send immediately back to the server.
//
//the mode in which the block shall operate is specified via the 
//input "i_Mode".
      NOP   0; 
// Evaluate the Life Sign Response Msg
      A     #t_Message_Received; // AND it is a new message      
      JCN   FA99; 

      A(    ; 
      L     #s_Last_Sent_Seq_Num; //IF the Life Sign Response message...
      L     #s_Msg_OPS_LifeSign.Sequence_Number; //has the correct sequence number
      ==I   ; 
      )     ; 
      NOT   ; 
      =     #t_Incorrect_Reply_MSG; 

      CLR   ; 
      =     #s_Wait_For_Reply; 

      L     0; //AND reset the LifeSigns missed counter
      T     #s_Timer_LifeSign_Reply; 

      AN    #t_Incorrect_Reply_MSG; 
      JCN   FA99; 

      R     #s_Communication_Error; //and reset the Communication Error

      L     0; 
      T     #s_Cnt_LifeSigns_Missed; 

FA99: NOP   0; 
NETWORK
TITLE =GA: Send lifesign request message

      NOP   0; 
// Send the Lifesign request message on OS from the timer.
      A     #t_OS_LifeSign_Interval; 
      A     #s_Done; 
      JCN   GA99; 

// Send LifeSignRequest message
// Update the sequence number
      L     #s_Last_Sent_Seq_Num; 
      +     1; 
      T     #s_Last_Sent_Seq_Num; 

      L     9999; 
      >I    ; 
      JCN   GA01; 
      L     0; 
      T     #s_Last_Sent_Seq_Num; 

// Send the message
GA01: CALL "FC_Crt_OPS_LifeSReq" (
           i_ID                     := #i_ID,
           i_FIFO_TCTC_DB_num       := #t_DB_Send_OPS_FIFO,
           i_Message_ID             := 200,
           i_Sequence_Number        := #s_Last_Sent_Seq_Num,
           i_DB_Event_Num           := #t_DB_Event_Num);

      SET   ; 
      =     #s_Wait_For_Reply; 
GA99: NOP   0; 
NETWORK
TITLE =HA: Check if error needs to be set
//Count the number of LifeSign Responses that was not received. Set the 
//communication error when a preset number of LifeSign responses was missed.
      NOP   0; 
//IF a LifeSign response has not been received in time
      A(    ; 
      A     #t_OS_LifeSign_Reply_Mis; 
      )     ; 
      O(    ; 
      A     #t_Incorrect_Reply_MSG; 
      A     #t_Message_Received; // AND it is a new message  
      )     ; 
      O(    ; 
      A     #t_OS_LifeSign_Interval; 
      )     ; 
      JCN   HA01; 

// Increase the LifeSign Missed Counter
      L     #s_Cnt_LifeSigns_Missed; 
      +     1; 
      T     #s_Cnt_LifeSigns_Missed; 

//IF the number of LifeSigns missed is equal to or higher than the number at which
//an error should be set, set the 'no communication' error.
HA01: L     #s_Cnt_LifeSigns_Missed; 
      L     #i_CFG_NumLifeSgnMiss_Err; 
      >=I   ; 
      S     #s_Communication_Error; 
NETWORK
TITLE =IA: Handle collected status
// 
      A     #s_Communication_Error; 
      =     #o_LifeSign_Err; 
END_FUNCTION_BLOCK

