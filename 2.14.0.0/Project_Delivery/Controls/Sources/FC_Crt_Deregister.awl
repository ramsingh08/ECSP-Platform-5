FUNCTION "FC_Crt_Deregister" : VOID
TITLE =%version: 0.14 %  CN: 32
//Function:
//Prepare and send message 'DeregisterPackage'
//
//History:
//This version  %created_by: nlhdl %
//              %date_created: maandag 7 december 2015 16:06:06 %
//              %release: VI_RAW/RA_Dev %
//
//Modification History:                                 By:      Date:     Rev:
//----------------------------------------------------  -------  --------  -----
//ra#9751: Add source files ECS-P to VI_RAW             nlHdL    14-12-15   0.14
//ra#9751: Add source files ECS-P to VI_RAW             nlHdL    07-12-15   0.14
//7324: ECS-P General from the RD to RA for platform    nlHdL    06-07-15   0.14
//23155: TIR in message for tubtrace                    nlHdL    24-04-15   0.14
//22337: Remove the vibes1.x with the vibes 3.x         nlHdL    08-01-15   0.13
//19518: Block call update of FC_xxx_Record             nltny    07-08-14   0.06
//18385: Added hand over reason and always use 1206     nlHHop   01-04-14   0.05
//17270: Check PID <> 0 when message to HLC             nlHdL    13-12-13   0.04
//15799: Block consistency update IO symbols, comment   nlHdL    23-09-13   0.03
//15170: Changed BTI to BTD for 4 digit Area num        nlHdL    13-08-13   0.02
//Initial Version based on version 0.08 ECS-P GEN       nlLvdG   31-05-13   0.01
//
//Description:
//Prepare and send message 'DeregisterPackage'
//
FAMILY : VIBES
VERSION : 0.0


VAR_INPUT
  i_ID : DWORD ;	//ID Record HEX AAAA - ZZSS/OO.
  i_Data : INT ;	//Data needed to send the message
  i_FIFO_PLC_DB_num : INT ;	//Number of DB PLC FIFO
  i_DB_Event_Num : INT ;	//Number of DB event
END_VAR
VAR_TEMP
  t_AR1_Backup : DINT ;	//Save the contents of AR1
  t_Ret_Val_Word : WORD ;	//Return value Word, not used
  t_Ret_Val_Int : INT ;	//Return value Integer, not Used
  t_LIC_Number : INT ;	//LIC number
  t_ECS_Number : INT ;	//ECS number
  t_Deregister_Reason : INT ;	//Reason for deregistration (1= Expected, 2= Unexpected, 3 = Hand over)
  t_TIC_Number : INT ;	//TIC number
  t_Expected_Item : BOOL ;	//Reason is expected
  t_Unexpected_Item : BOOL ;	//Reason is unexpected
  t_Hand_Over : BOOL ;	//Reason is handover
END_VAR
BEGIN
NETWORK
TITLE =AA: Get Date and Time

      TAR1  ; 
      T     #t_AR1_Backup; 

      CALL "READ_CLK" (
           RET_VAL                  := #t_Ret_Val_Int,
           CDT                      := "DB_Outgoing_Msgs".DeRegister.Body.EventTime);

NETWORK
TITLE =BA: Determine deregister reason

      NOP   0; 
// By default load 2 (unexpected) for the deregister reason
      L     2; 
      T     #t_Deregister_Reason; 

// Read ECS number from LNK list
      CALL "FC_Read_Int_Record" (
           i_Record_Number          := #i_Data,
           i_P_INT_Record           := "DB_UDT_LNK_Record".ECS,
           i_DB                     := "DB_LNK_List",
           i_DB_Event_Num           := #i_DB_Event_Num,
           o_INT                    := #t_ECS_Number);

// Check if ECS number is valid
      L     #t_ECS_Number; 
      L     0; 
      >I    ; 
      JCN   BA99; 

      CALL "FC_Read_Bool_Record" (
           i_Record_Number          := #t_ECS_Number,
           i_P_BOOL_Record          := "DB_UDT_ECS_Record".CIR.Expected_Item,
           i_DB                     := "DB_ECS_List",
           i_DB_Event_Num           := #i_DB_Event_Num,
           o_BOOL                   := #t_Expected_Item);

      CALL "FC_Read_Bool_Record" (
           i_Record_Number          := #t_ECS_Number,
           i_P_BOOL_Record          := "DB_UDT_ECS_Record".CIR.Unexpected_Item,
           i_DB                     := "DB_ECS_List",
           i_DB_Event_Num           := #i_DB_Event_Num,
           o_BOOL                   := #t_Unexpected_Item);

      CALL "FC_Read_Bool_Record" (
           i_Record_Number          := #t_ECS_Number,
           i_P_BOOL_Record          := "DB_UDT_ECS_Record".CIR.Hand_Over,
           i_DB                     := "DB_ECS_List",
           i_DB_Event_Num           := #i_DB_Event_Num,
           o_BOOL                   := #t_Hand_Over);

// Determine the deregister reason
      A     #t_Expected_Item; 
      JCN   BA01; 
      L     1; 
      JU    BA98; 
BA01: A     #t_Unexpected_Item; 
      JCN   BA02; 
      L     2; 
      JU    BA98; 
BA02: A     #t_Hand_Over; 
      JCN   BA99; 
      L     3; 
BA98: T     #t_Deregister_Reason; 

BA99: NOP   0; 

NETWORK
TITLE =CA: Write messageID

      AN    "M_Comm_Degraded"; 
      JCN   CA01; 

      L     206; // When NOT in degraded mode
      T     "DB_Outgoing_Msgs".DeRegister.Body.MessageID; 

      JU    CA99; 

CA01: L     1206; // When in degraded mode
      T     "DB_Outgoing_Msgs".DeRegister.Body.MessageID; 

CA99: NOP   0; 
NETWORK
TITLE =DA: Copy AreaID, ZoneID and SectionID

      L     #i_ID; // Load ID
      L     DW#16#FFFF0000; // Filter Area number only
      AD    ; 
      SRD   16; // Slide Area nubmer to beginning AAAA0000 -> AAAA
      BTD   ; // Convert HEX to INT
      T     "DB_Outgoing_Msgs".DeRegister.Body.AreaID; // Transfer to outgoing messages

      L     #i_ID; // Load ID
      L     DW#16#FF00; // Filter Zone number only
      AD    ; 
      SRD   8; // Slide Zone nubmer to beginning 0000ZZ00 -> 0000ZZ
      BTI   ; // Convert HEX to INT
      T     "DB_Outgoing_Msgs".DeRegister.Body.ZoneID; // Transfer to outgoing messages

      L     #i_ID; // Load ID
      L     DW#16#FF; // Filter Section number only
      AD    ; 
      BTI   ; // Convert HEX to INT
      T     "DB_Outgoing_Msgs".DeRegister.Body.EquipmentID; // Transfer to outgoing messages
NETWORK
TITLE =EA: Copy BIR and other required information into message

      NOP   0; 
// Read LIC number from LNK list
      CALL "FC_Read_Int_Record" (
           i_Record_Number          := #i_Data,
           i_P_INT_Record           := "DB_UDT_LNK_Record".LIC,
           i_DB                     := "DB_LNK_List",
           i_DB_Event_Num           := #i_DB_Event_Num,
           o_INT                    := #t_LIC_Number);

      L     #t_LIC_Number; 
      T     "DB_Outgoing_Msgs".DeRegister.Body.LIC; 

      L     #t_Deregister_Reason; 
      T     "DB_Outgoing_Msgs".DeRegister.Body.Reason; 

      CALL "FC_Read_ANY_Record" (
           i_Record_Number          := #t_LIC_Number,
           i_ANY_Data_Record        := "DB_UDT_LIC_Record".BIR,
           i_ANY_Data               := "DB_Outgoing_Msgs".DeRegister.Body.BIR,
           i_DB                     := "DB_LIC_List",
           i_DB_Event_Num           := #i_DB_Event_Num);

      L     "DB_Outgoing_Msgs".DeRegister.Body.BIR.PID; 
      L     0; 
      ==D   ; 
      JCN   EA01; 

      L     2206; // When PID = 0 then send log message deregister no PID
      T     "DB_Outgoing_Msgs".DeRegister.Body.MessageID; 

EA01: NOP   0; 
      L     0; 
      T     "DB_Outgoing_Msgs".DeRegister.Body.TIR.ID; 

      L     "DB_Setting".common.External_Log_Level; 
      L     4; 
      >I    ; 
      JCN   EA02; 

      CALL "FC_Read_Int_Record" (
           i_Record_Number          := #i_Data,
           i_P_INT_Record           := "DB_UDT_LNK_Record".TIC,
           i_DB                     := "DB_LNK_List",
           i_DB_Event_Num           := #i_DB_Event_Num,
           o_INT                    := #t_TIC_Number);

      CALL "FC_Read_ANY_Record" (
           i_Record_Number          := #t_TIC_Number,
           i_ANY_Data_Record        := "DB_UDT_TIC_Record".TIR,
           i_ANY_Data               := "DB_Outgoing_Msgs".DeRegister.Body.TIR,
           i_DB                     := "DB_TIC_List",
           i_DB_Event_Num           := #i_DB_Event_Num);

EA02: NOP   0; 
NETWORK
TITLE =FA: Put message in buffer

      CALL "BLKMOV" (
           SRCBLK                   := "DB_Outgoing_Msgs".Default,
           RET_VAL                  := #t_Ret_Val_Int,
           DSTBLK                   := "DB_Outgoing_Msgs".Merged_Message);

      CALL "BLKMOV" (
           SRCBLK                   := "DB_Outgoing_Msgs".DeRegister,
           RET_VAL                  := #t_Ret_Val_Int,
           DSTBLK                   := "DB_Outgoing_Msgs".Merged_Message);

      CALL "FC_FIFO" (
           i_FIFO_DB_Num            := #i_FIFO_PLC_DB_num,
           i_Mode                   := 'L',
           i_ANY_Data               := "DB_Outgoing_Msgs".Merged_Message,
           i_ID                     := #i_ID,
           o_RET_VAL_FIFO           := #t_Ret_Val_Word);

NETWORK
TITLE =GA: Restore AR1

      L     #t_AR1_Backup; 
      LAR1  ; 
END_FUNCTION

