FUNCTION_BLOCK "FB_EQP_Local_Control"
TITLE =%version: 0.05 % CN: 40
//Function:
//Equipment Local Control
//
//History:
//This version  %created_by: nlhdl %
//              %date_created: maandag 7 december 2015 16:10:24 %
//              %release: VI_RAW/RA_Dev %
//
//Modification History:                                   By:    Date:     Rev:
//----------------------------------------------------  -------  --------  -----
//ra#9751: Add source files ECS-P to VI_RAW             nlHdL    14-12-15   0.05
//ra#9751: Add source files ECS-P to VI_RAW             nlHdL    07-12-15   0.05
//7324: ECS-P General from the RD to RA for platform    nlHdL    06-07-15   0.05
//15807: Block consistency update IO symbols, comment   nlHdL    18-09-13   0.05
//CR19512 Update sources according S7 Manager           nlHdL    08-03-13   0.04
//Modified Input & Output names accord to Code Conven   inPNa    15-09-10   0.03
//PR5430: Remove i_ID, because not used                 nlMRe    27-07-10   0.02
//Initial version                                       inPNa    08-05-10   0.01
//
//Description:
//- Generate the Start & Reset request depends on the  request from above layer.
//- When Restart is detected then in 1st scan cycle 'RESET' request is 
//generated & in 2nd cycle 'START' Request generated. 
//
FAMILY : General
VERSION : 0.0


VAR_INPUT
  i_CMD_Start : BOOL ;	//CMD: Start command
  i_CMD_Reset : BOOL ;	//CMD: Reset command
  i_CMD_Not_Restart : BOOL ;	//CMD: Restart without Reset
END_VAR
VAR_OUTPUT
  o_CMD_Start_Request : BOOL ;	//CMD: Start one-short for components
  o_CMD_Reset_Request : BOOL ;	//CMD: Reset one-short for components
END_VAR
VAR
  s_FP_Start_Request : BOOL ;	
  s_FP_Reset_Request : BOOL ;	
  s_FN_Reset_Request : BOOL ;	
  s_FN_Reset_Request1 : BOOL ;	
  s_1_Cycle_Memory : BOOL ;	
  s_FP_1_Cycle_Memory : BOOL ;	
END_VAR
BEGIN
NETWORK
TITLE =AA: Handle Start Request
//One shot start request is made here. 
//Start Request from above layer.
//After Restart is detected then in 2nd scan cycle one-shot pulse for start 
//request.
//Reset the 1st cycle memory
      A     #i_CMD_Start; //IF Start request
      FP    #s_FP_Start_Request; //FP Start request
      O     ; //OR
      A     #s_1_Cycle_Memory; //IF Stat-Memory 1st Cycle Memory (After Restart is detected)
      FP    #s_FP_1_Cycle_Memory; //FP Stat-Memory 1st cycle Memory1
      =     #o_CMD_Start_Request; //ASSIGN Output to Start request
      R     #s_1_Cycle_Memory; //RESET Stat-Memory 1st cycle Memory
NETWORK
TITLE =BA: Handle Reset request
//One shot Reset request is made here
//Reset Request from above layer.
//When Restart is detected then in 1st cycle Reset command is given
      A     #i_CMD_Reset; //IF Reset request
      FP    #s_FP_Reset_Request; //FP Stat-Memory reset request
      O     ; //OR
      A     #i_CMD_Not_Restart; //IF Restart detected
      FN    #s_FN_Reset_Request; //FN  Reset request
      =     #o_CMD_Reset_Request; //THEN make One shot Reset
NETWORK
TITLE =CA: Memory of 1st PLC scan after Restart is detected
//Restart is detected to memorise the 1st cycle.
      A     #i_CMD_Not_Restart; //IF Restart detected
      FN    #s_FN_Reset_Request1; //FN  Reset request
      S     #s_1_Cycle_Memory; //THEN SET 1_Cycle memory
END_FUNCTION_BLOCK

