FUNCTION "FC_Crt_PackReScan" : VOID
TITLE =%version: 0.02 % CN: 32
//Function:
//Prepare and send message 'PackageReport Reclaimedscanned' - BSO result
//
//History:
//This version  %created_by: nlhdl %
//              %date_created: Thursday, January 05, 2017 2:46:39 PM %
//              %release: VI_GEN/RA_Dev %
//
//Modification History:                                 By:      Date:     Rev:
//----------------------------------------------------  -------  --------  -----
//ra#19666: Message ID 2213 PID=0 not correct           nlHdL    05-01-17   0.02
//Initial Version based on 0.20 FC_Crt_PackScanned      nlHdL    10-08-16   0.01
//
//Description:
//Prepare and send message 'PackageReport Reclaimedscanned' - BSO result
//
FAMILY : VIBES
VERSION : 0.0


VAR_INPUT
  i_ID : DWORD ;	//ID Record HEX AAAA - ZZSS/OO.
  i_Data : INT ;	//Data needed to send the message
  i_FIFO_PLC_DB_num : INT ;	//Number of DB PLC FIFO
  i_DB_Event_Num : INT ;	//Number of DB event
END_VAR
VAR_TEMP
  t_AR1_Backup : DINT ;	//Save the contents of AR1
  t_Ret_Val_Word : WORD ;	//Return value Word, not used
  t_Ret_Val_Int : INT ;	//Return value Integer, not Used
  t_LIC : INT ;	//The LIC record used to create the message and send to SAC
  t_TIC_Number : INT ;	//TIC number
END_VAR
BEGIN
NETWORK
TITLE =AA: Store AR1 and Get Date and Time

      TAR1  ; // Store AR1
      T     #t_AR1_Backup; 

      CALL "READ_CLK" (
           RET_VAL                  := #t_Ret_Val_Int,
           CDT                      := "DB_Outgoing_Msgs".PackageReport_Scan.body.EventTime);

NETWORK
TITLE =BA: Handle LNK code

      CALL "FC_Read_Int_Record" (// Read the LIC number from the Link list
           i_Record_Number          := #i_Data,
           i_P_INT_Record           := "DB_UDT_LNK_Record".LIC,
           i_DB                     := "DB_LNK_List",
           i_DB_Event_Num           := #i_DB_Event_Num,
           o_INT                    := #t_LIC);

      CALL "FC_Read_ANY_Record" (
           i_Record_Number          := #t_LIC,
           i_ANY_Data_Record        := "DB_UDT_LIC_Record".BIR.PID,
           i_ANY_Data               := "DB_Outgoing_Msgs".PackageReport_Scan.body.BIR.PID,
           i_DB                     := "DB_LIC_List",
           i_DB_Event_Num           := #i_DB_Event_Num);

NETWORK
TITLE =CA: Write messageID

      AN    "M_Comm_Degraded"; // When in degraded mode
      JCN   CA01; 

      L     213; // When NOT in degraded mode
      T     "DB_Outgoing_Msgs".PackageReport_Scan.body.MessageID; 

      JU    CA99; 

CA01: L     1213; // When in degraded mode
      T     "DB_Outgoing_Msgs".PackageReport_Scan.body.MessageID; 

CA99: NOP   0; 
NETWORK
TITLE =DA: Copy AreaID, ZoneID and SectionID

      L     #i_ID; // Load ID
      L     DW#16#FFFF0000; // Filter Area number only
      AD    ; 
      SRD   16; // Slide Area nubmer to beginning AAAA0000 -> AAAA
      BTD   ; // Convert HEX to INT
      T     "DB_Outgoing_Msgs".PackageReport_Scan.body.AreaID; // Transfer to outgoing messages

      L     #i_ID; // Load ID
      L     DW#16#FF00; // Filter Zone number only
      AD    ; 
      SRD   8; // Slide Zone nubmer to beginning 0000ZZ00 -> 0000ZZ
      BTI   ; // Convert HEX to INT
      T     "DB_Outgoing_Msgs".PackageReport_Scan.body.ZoneID; // Transfer to outgoing messages

      L     #i_ID; // Load ID
      L     DW#16#FF; // Filter Section number only
      AD    ; 
      BTI   ; // Convert HEX to INT
      T     "DB_Outgoing_Msgs".PackageReport_Scan.body.EquipmentID; // Transfer to outgoing messages
NETWORK
TITLE =EA: Handle LIC code

      L     #t_LIC; // Write LIC number in the message
      T     "DB_Outgoing_Msgs".PackageReport_Scan.body.LIC; 
NETWORK
TITLE =GA: Copy Information Record into message

      CALL "FC_Read_ANY_Record" (
           i_Record_Number          := #t_LIC,
           i_ANY_Data_Record        := "DB_UDT_LIC_Record".BIR,
           i_ANY_Data               := "DB_Outgoing_Msgs".PackageReport_Scan.body.BIR,
           i_DB                     := "DB_LIC_List",
           i_DB_Event_Num           := #i_DB_Event_Num);

      L     "DB_Outgoing_Msgs".PackageReport_Scan.body.BIR.PID; 
      L     0; 
      ==D   ; 
      JCN   GA01; 

      L     2213; // When PID = 0 then send log messagePackageReport no pid
      T     "DB_Outgoing_Msgs".PackageReport_Scan.body.MessageID; 

GA01: NOP   0; 
      L     0; 
      T     "DB_Outgoing_Msgs".PackageReport_Scan.body.TIR.ID; 

      L     "DB_Setting".common.External_Log_Level; 
      L     4; 
      >I    ; 
      JCN   GA02; 

      CALL "FC_Read_Int_Record" (
           i_Record_Number          := #i_Data,
           i_P_INT_Record           := "DB_UDT_LNK_Record".TIC,
           i_DB                     := "DB_LNK_List",
           i_DB_Event_Num           := #i_DB_Event_Num,
           o_INT                    := #t_TIC_Number);

      CALL "FC_Read_ANY_Record" (
           i_Record_Number          := #t_TIC_Number,
           i_ANY_Data_Record        := "DB_UDT_TIC_Record".TIR,
           i_ANY_Data               := "DB_Outgoing_Msgs".PackageReport_Scan.body.TIR,
           i_DB                     := "DB_TIC_List",
           i_DB_Event_Num           := #i_DB_Event_Num);

GA02: NOP   0; 

NETWORK
TITLE =HA: Copy LPC's

      CALL "FC_Read_ANY_Record" (
           i_Record_Number          := #t_LIC,
           i_ANY_Data_Record        := "DB_UDT_LIC_Record".Status.Scanresult,
           i_ANY_Data               := "DB_Outgoing_Msgs".PackageReport_Scan.body.ScanResult,
           i_DB                     := "DB_LIC_List",
           i_DB_Event_Num           := #i_DB_Event_Num);
NETWORK
TITLE =IA: Put message in buffer

      CALL "BLKMOV" (
           SRCBLK                   := "DB_Outgoing_Msgs".Default,
           RET_VAL                  := #t_Ret_Val_Int,
           DSTBLK                   := "DB_Outgoing_Msgs".Merged_Message);

      CALL "BLKMOV" (
           SRCBLK                   := "DB_Outgoing_Msgs".PackageReport_Scan,
           RET_VAL                  := #t_Ret_Val_Int,
           DSTBLK                   := "DB_Outgoing_Msgs".Merged_Message);

      CALL "FC_FIFO" (
           i_FIFO_DB_Num            := #i_FIFO_PLC_DB_num,
           i_Mode                   := 'L',
           i_ANY_Data               := "DB_Outgoing_Msgs".Merged_Message,
           i_ID                     := #i_ID,
           o_RET_VAL_FIFO           := #t_Ret_Val_Word);

NETWORK
TITLE =JA: Restore AR1

      L     #t_AR1_Backup; 
      LAR1  ; 
END_FUNCTION

