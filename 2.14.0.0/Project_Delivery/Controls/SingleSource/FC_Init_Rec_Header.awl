FUNCTION "FC_Init_Rec_Header" : VOID
TITLE =%version: 0.04 % CN: 31 
//Function:
//Write the default DB number into the Header, and the entries present
//
//History:                                                                        
//This version   %created_by: nlhdl %
//              %date_created: maandag 7 december 2015 16:05:05 %
//              %release: VI_RAW/RA_Dev %
//
//Modification History:                                   By:    Date:     Rev:
//----------------------------------------------------  -------  --------  -----
//ra#9751: Add source files ECS-P to VI_RAW             nlHdL    14-12-15   0.04
//ra#9751: Add source files ECS-P to VI_RAW             nlHdL    07-12-15   0.04
//7324: ECS-P General from the RD to RA for platform    nlHdL    06-07-15   0.04
//15797: Keyword release added                          nlwwij   26-09-13   0.04
//PR6408 Adapted for bigger amount of record            nlple    28-02-11   0.03
//Remove author and name fields                         nlMRe    22-09-10   0.02
//Initial revision                                      inPNa    09-09-10   0.01
//
//Description:
//Calculated the length of the UDT and write this length into 
//the global DB. Write the default DB number into the Header, and the entries 
//present.
//
FAMILY : General
VERSION : 0.0


VAR_INPUT
  i_DB : BLOCK_DB ;	//Block DB of global DB to write UDT length to
  i_DB_UDT : BLOCK_DB ;	//Block DB of UDT to calculated the length of each record
  i_DB_Default_Record : BLOCK_DB ;	//Block DB of global DB of default records for the item
END_VAR
VAR_TEMP
  t_Length : INT ;	//Length of UDT
  t_DB_Number : INT ;	//DB number of the block
END_VAR
BEGIN
NETWORK
TITLE =AA: Check current length value

      OPN   #i_DB; 
      A     DBX   10.0; // IF Done bit is set end block
      BEC   ; 
NETWORK
TITLE =BA: Calculated the record length

      OPN   #i_DB_UDT; //Open this DB
      L     DBLG; //and determine the length, which is also the length of the record
      T     #t_Length; 

      OPN   #i_DB; 
      L     #t_Length; 
      T     DBW    2; // Length of each record

      L     DBLG; 
      L     DBW    0; // Load header length
      -D    ; 
      L     DBW    2; 
      /D    ; 
      T     DBW    6; // Entries present within List DB

      SET   ; 
      S     DBX   10.0; // Set s_Done into DB

NETWORK
TITLE =CA: Write DB number of default record

      OPN   #i_DB_Default_Record; //Open this DB
      L     DBNO; //and determine the length, which is also the length of the record
      T     #t_DB_Number; 

      L     #t_DB_Number; 
      L     2; 
      >I    ; 
      JCN   CA99; 

      OPN   #i_DB; 
      L     #t_DB_Number; 
      T     DBW    8; // DB number of default record

CA99: NOP   0; 
END_FUNCTION

