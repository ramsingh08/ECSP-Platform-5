FUNCTION "FC_Clear_Bool_DB" : VOID
TITLE =%version: 0.04 %  CN: 30
//Function:
//This clears all bools in a specific table.
//
//History:
//This version  %created_by: nlhdl %
//              %date_created: Thursday, June 08, 2017 2:52:47 PM %
//              %release: VI_GEN/RA_Dev %
//
//Modification History:                                   By:    Date:     Rev:
//----------------------------------------------------  -------  --------  -----
//ra#22580: Check DB length for Msgs with tableindex    nlHdL    08-06-17   0.04
//ra#9751: Add source files ECS-P to VI_RAW             nlHdL    14-12-15   0.03
//ra#9751: Add source files ECS-P to VI_RAW             nlHdL    07-12-15   0.03
//7324: ECS-P General from the RD to RA for platform    nlHdL    06-07-15   0.03
//15797: Keyword release added                          nlwwij   26-09-13   0.03
//CR19512 Update sources according S7 Manager           nlHdL    07-03-13   0.02
//Initial Revision                                      nlMRe    08-09-10   0.01
//
//Description:
//This clears all bools in a specific table.
//
FAMILY : General
VERSION : 0.0


VAR_INPUT
  i_TableDB : BLOCK_DB ;	//Table in which data can be found
END_VAR
VAR_TEMP
  t_AR1_Backup : DWORD ;	//Backup of AR1 content
  t_Loop_Counter : INT ;	//Number of entries in TableDB
END_VAR
BEGIN
NETWORK
TITLE =AA: Save the contents of the address register AR1

      TAR1  #t_AR1_Backup; 

NETWORK
TITLE =BA: Open Table

      NOP   0; 
// OpenTable DB and calculate the number of entries
      OPN   #i_TableDB; 

      L     DBLG; 
      L     6; //one entry is 6 bytes long
      /D    ; 
      T     #t_Loop_Counter; 
      L     0; 
      ==I   ; 
      BEC   ; // No entries present

// Set a pointer to the first entry
      L     P#DBX 0.0; 
      LAR1  ; 
      JU    BA02; 

// Set the pointer to the next entry
BA01: L     P#6.0; 
      +AR1  ; 

// Decrease the loop counter
BA02: L     #t_Loop_Counter; 
      L     1; 
      -I    ; 
      T     #t_Loop_Counter; 

//Clear the data
      CLR   ; 
      =      [AR1,P#4.0]; 

// Check loop counter
      L     #t_Loop_Counter; 
      L     0; 
      <=I   ; 
      JCN   BA01; 
NETWORK
TITLE =CA: Restore the contents of the address register AR1

      LAR1  #t_AR1_Backup; 

END_FUNCTION

