FUNCTION "FC_PLT_Clr_DB" : VOID
TITLE =%version: 1.02 % CN: 32
//Function:
//Clear complete PLT DB.
//The Data which is cleared is report via events.
//
//History:
//This version  %created_by: nlhdl %
//              %date_created: Friday, February 10, 2017 1:46:25 PM %
//              %release: VI_GEN/RA_Dev %
//
//Modification History:                                   By:    Date:     Rev:
//----------------------------------------------------  -------  --------  -----
//ra#20140: Clear IHO Bit within PLT Clear DB           nlHdL    10-02-17   1.02
//ra#9751: Add source files ECS-P to VI_RAW             nlHdL    14-12-15   1.01
//ra#9751: Add source files ECS-P to VI_RAW             nlHdL    07-12-15   1.01
//7324: ECS-P General from the RD to RA for platform    nlHdL    06-07-15   1.01
//15807: Block consistency update IO symbols, comment   nlHdL    18-09-13   1.01
//13971: Modified PLT concept with state Version1       nlHdL    29-05-13   1.00
// Init version based on version 0.05 of PLT version 0
//
//Description:
//This function block is able to clear a part of the PLT DB on the position 
//with the defined window.
//
//
//                      Position     
//                         |<--window-->|
//         ____________________________________                                   
//              
//                 ===flow-direction===>                                          
//          
//         ____________________________________                                   
//                         ^          ^
//                         |          |
//               i_Window  <----------|
//                                i_Position     
//                                               
//                                                                          
FAMILY : General
VERSION : 0.0


VAR_INPUT
  i_Enable_Clr : BOOL ;	//Enable the function to clear the PLT DB
  i_DB_PLT_Num : INT ;	//Number of DB Position Leading Trailing Edge
  i_DB_Event_Num : INT ;	//Number of DB event
END_VAR
VAR_TEMP
  t_PLT_DB_Nr : INT ;	//Copy of i_DB_Num_PLT
  t_Cnt_Entry : INT ;	//Counter for number of entries searched
  t_Link : INT ;	//Read Link number
  t_Backup_AR1 : DWORD ;	//Backup of AR1 (needed because of internal FC Call)
  t_Ret_Event : INT ;	//Return value for Write Event
  t_PEC_Latch_PLT : BOOL ;	//PEC Latch for PLT
END_VAR
BEGIN
NETWORK
TITLE =AA: If not enabled end block

      AN    #i_Enable_Clr; 
      BEC   ; 
NETWORK
TITLE =BA: Open PLT DB
// 
      L     #i_DB_PLT_Num; 
      T     #t_PLT_DB_Nr; 
      OPN   DB [#t_PLT_DB_Nr]; 

      A     DBX   10.7; 
      =     #t_PEC_Latch_PLT; 

      AN    #t_PEC_Latch_PLT; 
      R     DBX   10.6; //Reset Item in Handover if no PEC Latch
NETWORK
TITLE =CA: Check if entries are filled in PLT DB
// 
      L     DBW    0; 
      L     0; // Number of entry,s used in PLT DB
      <=I   ; 
      BEC   ; // end function
NETWORK
TITLE =DA: Counter Entry used  
// 
      L     DBW    0; // Number of entries used in PLT DB
      T     #t_Cnt_Entry; // Copy used entries
NETWORK
TITLE =EA: Delete entries.

      LAR1  P#14.0; // Pointer to first entry (DBW14)
      TAR1  ; 
      T     #t_Backup_AR1; 

EA01: L     DBW [AR1,P#6.0]; 
      T     #t_Link; 

      L     #t_Link; // IF data = (UFO)PIC
      L     0; 
      >I    ; 
      JCN   EA02; 

      CALL "FC_Write_Event" (// THEN report event "PIC Lost due to clear PLT" (3801)
           i_Message_ID             := 3801,
           i_Event_value            := #t_Link,
           i_DB_Event_Num           := #i_DB_Event_Num,
           o_Return_Value           := #t_Ret_Event);

      CALL "FC_Write_Event" (// Report PLT DB number (3803)
           i_Message_ID             := 3803,
           i_Event_value            := #t_PLT_DB_Nr,
           i_DB_Event_Num           := #i_DB_Event_Num,
           o_Return_Value           := #t_Ret_Event);

//Restore AR1
      L     #t_Backup_AR1; 
      LAR1  ; 

EA02: L     0; 
      T     DBD [AR1,P#0.0]; 
      T     DBD [AR1,P#4.0]; 

//Pointer to next entrie
      L     #t_Backup_AR1; 
      L     P#8.0; // 6 bytes for each record increase pointer with 1 entry.
      +D    ; // Entry pointer = Entry pointer - P#8.0
      LAR1  ; // Store Entry pointer in address-register 
      TAR1  ; 
      T     #t_Backup_AR1; 

//Loop to next entry
      L     #t_Cnt_Entry; 
      L     1; 
      -I    ; 
      T     #t_Cnt_Entry; 
      JZ    EA04; 

      JU    EA01; // GOTO START OF LOOP

//Write no entries are used 
EA04: L     0; 
      T     DBW    0; 
END_FUNCTION

