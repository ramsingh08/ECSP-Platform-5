FUNCTION "FC_Snd_InitSync" : VOID
TITLE =%version: 0.04 %  CN: 40
//Function:
//Prepare and send message 'LifeSignRequest'
//
//History:
//This version  %created_by: nlhdl %
//              %date_created: maandag 7 december 2015 16:13:28 %
//              %release: VI_RAW/RA_Dev %
//
//Modification History:                                 By:      Date:     Rev:
//----------------------------------------------------  -------  --------  -----
//ra#9751: Add source files ECS-P to VI_RAW             nlHdL    14-12-15   0.04
//ra#9751: Add source files ECS-P to VI_RAW             nlHdL    07-12-15   0.04
//7324: ECS-P General from the RD to RA for platform    nlHdL    06-07-15   0.04
//23966: Added or create ID from FIFO block             nlHdL    18-06-15   0.04
//15799: Block consistency update IO symbols, comment   nlHdL    23-09-13   0.02
//Initial Version                                       nlLvdG   31-05-13   0.01
//
//Description:
//Prepare and send message 'LifeSignRequest'
//
FAMILY : VIBES
VERSION : 0.0


VAR_INPUT
  i_DB_send_FIFO : INT ;	//DB send FIFO
  i_AreaID : INT ;	//AreaID
  i_ProcessID : INT ;	//Process ID
  i_MessageID : INT ;	//MessageID to use
END_VAR
VAR_TEMP
  t_Ret_Val_Word : WORD ;	//Return value
  t_Ret_Val_Int : INT ;	//Return Value
  t_ID_Area : DWORD ;	//Area ID for report
END_VAR
BEGIN
NETWORK
TITLE =AA: Get Date and Time

      CALL "READ_CLK" (
           RET_VAL                  := #t_Ret_Val_Int,
           CDT                      := "DB_Outgoing_Msgs".InitSync.EventTime);

NETWORK
TITLE =BA: Write messageID

      L     #i_MessageID; 
      T     "DB_Outgoing_Msgs".InitSync.MessageID; 
NETWORK
TITLE =CA: Copy AreaID

      L     #i_AreaID; 
      T     "DB_Outgoing_Msgs".InitSync.AreaID; 

      L     #i_AreaID; 
      DTB   ; 
      SLD   16; 
      T     #t_ID_Area; 
NETWORK
TITLE =DA: Copy ProcessID

      L     #i_ProcessID; 
      T     "DB_Outgoing_Msgs".InitSync.Process_ID; 
NETWORK
TITLE =EA: Put message in buffer

      CALL "BLKMOV" (
           SRCBLK                   := "DB_Outgoing_Msgs".Default,
           RET_VAL                  := #t_Ret_Val_Int,
           DSTBLK                   := "DB_Outgoing_Msgs".Merged_Message);

      CALL "BLKMOV" (
           SRCBLK                   := "DB_Outgoing_Msgs".InitSync,
           RET_VAL                  := #t_Ret_Val_Int,
           DSTBLK                   := "DB_Outgoing_Msgs".Merged_Message);

      CALL "FC_FIFO" (
           i_FIFO_DB_Num            := #i_DB_send_FIFO,
           i_Mode                   := 'L',
           i_ANY_Data               := "DB_Outgoing_Msgs".Merged_Message,
           i_ID                     := #t_ID_Area,
           o_RET_VAL_FIFO           := #t_Ret_Val_Word);

END_FUNCTION

