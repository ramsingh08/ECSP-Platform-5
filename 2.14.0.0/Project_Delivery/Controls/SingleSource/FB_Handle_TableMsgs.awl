FUNCTION_BLOCK "FB_Handle_TableMsgs"
TITLE =%version: 0.06 %  CN: 60
//Function:
//Handle received message.
//
//Description:
//This FB handles the received message. It divides the received message depending 
//on message characteristic over the specific message handlers.
//
//History:
//This version  %created_by: nlhdl %
//              %date_created: maandag 7 december 2015 16:15:55 %
//              %release: VI_RAW/RA_Dev %
//
//Modification History:                                 By:      Date:     Rev:
//----------------------------------------------------  -------  --------  -----
//ra#9751: Add source files ECS-P to VI_RAW             nlHdL    14-12-15   0.06
//ra#9751: Add source files ECS-P to VI_RAW             nlHdL    07-12-15   0.06
//7530: Chaneg MD_DWORD_To_Do to AreaID as report ID    nlHdL    20-07-15   0.06
//7324: ECS-P General from the RD to RA for platform    nlHdL    06-07-15   0.05
//18060: Update messages ID's for Vibes 3.2             nlHdL    19-02-14   0.04
//15799: Block consistency update IO symbols, comment   nlHdL    23-09-13   0.03
//Change Received msg UDT for Tables                    nlLvdG   04-06-13   0.02
//Initial Version                                       nlLvdG   31-05-13   0.01
//
FAMILY : VIBES
VERSION : 0.0


VAR_INPUT
  i_AreaID : INT ;	//SETT: AreaID of this Area
  i_DB_Recv_FIFO : BLOCK_DB ;	//SETT: DB of used Receive FIFO
  i_DB_Event : BLOCK_DB ;	//CFG: Local Event DB
END_VAR
VAR
  s_FB_Rcv_RoutingTabUp : "FB_Rcv_RoutingTabUp";	
  s_FB_Rcv_HandAreaTabUp : "FB_Rcv_HandAreaTabUp";	
  s_Received_Message : "UDT_Msg_RcvT_Body";	//Received Message
  s_RET_VAL_Recv_FIFO : WORD ;	//Return value in WORD format
  s_Recv_FIFO_DBNr : INT ;	//DB number of used Receive FIFO DB 
  s_Message_Received : BOOL ;	//One shot signal when a new message is received
END_VAR
VAR_TEMP
  t_Event_DB_Num : INT ;	//Number of the local event DB
  t_ID_Area : DWORD ;	//Area ID number for report
END_VAR
BEGIN
NETWORK
TITLE =AA: Check FIFO for new messages

      OPN   #i_DB_Event; // Determin event DB nr
      L     DBNO; 
      T     #t_Event_DB_Num; 

      L     #i_AreaID; 
      DTB   ; 
      SLD   16; 
      T     #t_ID_Area; 

      OPN   #i_DB_Recv_FIFO; //Determine send FIFO's DB nr
      L     DBNO; 
      T     #s_Recv_FIFO_DBNr; 

      AN    DBX    0.0; // When FIFO is not empty (empty bit is first bit in FIFO DB)
      =     #s_Message_Received; 
      JCN   AA01; 

      CALL "FC_FIFO" (// Retrieve one message from Receive FIFO
           i_FIFO_DB_Num            := #s_Recv_FIFO_DBNr,
           i_Mode                   := 'O',
           i_ANY_Data               := #s_Received_Message,
           i_ID                     := #t_ID_Area,
           o_RET_VAL_FIFO           := #s_RET_VAL_Recv_FIFO);

AA01: NOP   0; 
NETWORK
TITLE =BA: RoutingTableUpdate ('101')

      L     #s_Received_Message.MessageID; 
      L     101; 
      ==I   ; 
      A     #s_Message_Received; 
      JCN   BA99; 

      CALL #s_FB_Rcv_RoutingTabUp (
           i_ANY_RCV_Msg_Body       := #s_Received_Message);

BA99: NOP   0; 
NETWORK
TITLE =CA: HandlingAreaTableUpdate ('105')

      L     #s_Received_Message.MessageID; 
      L     105; 
      ==I   ; 
      A     #s_Message_Received; 
      JCN   CA99; 

      CALL #s_FB_Rcv_HandAreaTabUp (
           i_ANY_RCV_Msg_Body       := #s_Received_Message,
           i_DB_Event_Num           := #t_Event_DB_Num);

CA99: NOP   0; 
END_FUNCTION_BLOCK

