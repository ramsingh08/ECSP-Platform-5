FUNCTION_BLOCK "FB_Handle_NormTfcMsg"
TITLE =%version: 1.09 %  CN: 60
//Function:
//Handle received message.
//
//History:
//This version  %created_by: nlhdl %
//              %date_created: Thursday, January 18, 2018 11:55:14 AM %
//              %release: VI_GEN/RA_Dev %
//
//Modification History:                                 By:      Date:     Rev:
//----------------------------------------------------  -------  --------  -----
//ra#27040: Remove StatusReport from library            nlHdL    18-01-18   1.09
//ra#22601: Updates required ICS maintenance req/rep    nlJMol   04-05-17   1.08
//ra#18111: Implement ICS trace to the GEN Libray       nlHdL    20-02-17   1.07
//ra#16574: rename block FB_Handle_NormTfcMsg           nlHdL    14-09-16   1.06
//ra#13418: FB_Handle_NormalMsgs for TFC wrong version  nlHdL    26-04-16   1.05
//ra#11479: Add TransportDirReport to 2 dir merge zone  nlHdL    03-03-16   1.05
//9241: Remove To_Do merker TFC handle messages.        nlHdL    29-10-15   1.04
//7632: Add TFC blocks to platform 4                    nlHdL    30-07-15   1.03
//20552: FB_Handle_NormalMsgs_TFC with error lifesigns  nlHdL    08-09-14   1.03
//20451: Wrong Area ID conversion before status report  nlHdL    11-08-14   0.02
//18060: Update messages ID's for Vibes 3.2             nlHdL    31-03-14   0.01
//Initial Version based on version 0.04 TTC             nlHdL    26-02-14   0.01
//
//Description:
//This FB handles the received message. It divides the received message depending 
//on message characteristic over the specific message handlers.
//
FAMILY : VIBES
VERSION : 0.0


VAR_INPUT
  i_DB_Recv_FIFO : BLOCK_DB ;	//Data Structure: TCTC DB receive FIFO
  i_DB_Send_FIFO : BLOCK_DB ;	//Data Structure: DB send FIFO
  i_DB_Event : BLOCK_DB ;	//Data Structure: Event DB
  i_CFG_LifeSignReq_Interv : INT ;	//CFG: [sec] Interval for sending a LifeSignRequest msg (0 = send no LifeSigns)
  i_CFG_LifeSignResp_Time : INT ;	//CFG: [sec] Time after sending Request that Response must be received
  i_CFG_NumLifeSgnMiss_Err : INT ;	//CFG: [Num] Number of Lifesign responses to be missed before an error is set
  i_CFG_AreaID : INT ;	//CFG: Area ID number
  i_CMD_Degraded_Mode : BOOL ;	//CMD: Degraded mode
END_VAR
VAR_OUTPUT
  o_Err_No_Lifesigns : BOOL ;	//DS: A predefined number of LifeSignResponses have not been received
  o_Err_No_Lifesigns_TFC : BOOL ;	//DS: A predefined number of LifeSignResponses TFC have not been received
END_VAR
VAR
  s_FB_Rcv_PackageInstr : "FB_Rcv_PackageInstr";	//Data Structure:
  s_FB_Rcv_CapacityInstr : "FB_Rcv_CapacityInstr";	//Data Structure:
  s_FB_LS : "FB_LifeSign";	//Data Structure: //Data Structure:
  s_FB_LS_TFC : "FB_LifeSign";	//Data Structure: //Data Structure:
  s_FB_EmtyLaneInstr : "FB_Rcv_EmtyLaneInstr";	//Data Structure:
  s_FB_Rcv_ItemRequest : "FB_Rcv_ItemRequest";	//Data Structure:
  s_FB_Rcv_SrtCtrlTabUp : "FB_Rcv_SrtCtrlTab_T";	//Data Structure:	
  s_FB_Rcv_RelCtrlTabUp : "FB_Rcv_RelCtrlTab_T";	//Data Structure:
  s_FB_Rcv_ICSMaintReq : "FB_Rcv_ICSMaintReq";	//Data Structure 
  s_FB_Rcv_ICSMaintRep : "FB_Rcv_ICSMaintRep";	//Data Structure 
  s_Received_Message : "UDT_Msg_Rcv_Body";	//Data Structure: Received Message
  s_RET_VAL_Recv_FIFO : WORD ;	//Return value in WORD format
  s_Recv_FIFO_DBNr : INT ;	//DB number of used Receive FIFO DB 
  s_Send_FIFO_DBNr : INT ;	//DB number of used Send FIFO DB
  s_Message_Received : BOOL ;	//One shot signal when a new message is received
END_VAR
VAR_TEMP
  t_Event_DB_Num : INT ;	//Number of the local event DB
  t_Comm_OS_InitSync_1 : BOOL ;	
  t_ID_Area : DWORD ;	
END_VAR
BEGIN
NETWORK
TITLE =AA: Check FIFO for new messages

      OPN   #i_DB_Event; // Determin event DB nr
      L     DBNO; 
      T     #t_Event_DB_Num; 

      L     #i_CFG_AreaID; 
      DTB   ; 
      SLD   16; 
      T     #t_ID_Area; 

      OPN   #i_DB_Send_FIFO; // Determine send FIFO's DB nr
      L     DBNO; 
      T     #s_Send_FIFO_DBNr; 

      OPN   #i_DB_Recv_FIFO; // Determine receive FIFO's DB nr
      L     DBNO; 
      T     #s_Recv_FIFO_DBNr; 

      AN    DBX    0.0; // When FIFO is not empty (empty bit is first bit in FIFO DB)
      =     #s_Message_Received; 
      JCN   AA01; 

      CALL "FC_FIFO" (// Retrieve one message from Receive FIFO
           i_FIFO_DB_Num            := #s_Recv_FIFO_DBNr,
           i_Mode                   := 'O',
           i_ANY_Data               := #s_Received_Message,
           i_ID                     := #t_ID_Area,
           o_RET_VAL_FIFO           := #s_RET_VAL_Recv_FIFO);

AA01: NOP   0; 
NETWORK
TITLE =BA: LifeSignResponse('2')

      CALL #s_FB_LS (
           i_ANY_RCV_Msg_Body       := #s_Received_Message,
           i_CFG_LifeSignReq_Interv := #i_CFG_LifeSignReq_Interv,
           i_CFG_LifeSignResp_Time  := #i_CFG_LifeSignResp_Time,
           i_CFG_NumLifeSgnMiss_Err := #i_CFG_NumLifeSgnMiss_Err,
           i_CFG_AreaID             := #i_CFG_AreaID,
           i_CFG_LifeSignReq_MsgID  := 1,
           i_CFG_LifeSignResp_MsgID := 2,
           i_InitSync_MsgID         := 3,
           i_DB_Num_Send_FIFO       := #s_Send_FIFO_DBNr,
           i_Message_Received       := #s_Message_Received,
           i_Degraded_Mode          := #i_CMD_Degraded_Mode,
           o_Err_No_Lifesigns       := #o_Err_No_Lifesigns,
           o_OS_Comm_Restored       := "M_Comm_OS_InitSync");

NETWORK
TITLE =CA: PackageInstruction ('102')

      L     #s_Received_Message.MessageID; 
      L     102; 
      ==I   ; 
      A     #s_Message_Received; 
      JCN   CA99; 

      CALL #s_FB_Rcv_PackageInstr (
           i_ANY_RCV_Msg_Body       := #s_Received_Message,
           i_DB_Event_Num           := #t_Event_DB_Num);

CA99: NOP   0; 
NETWORK
TITLE =DA: CapacityInstruction ('103')

      L     #s_Received_Message.MessageID; 
      L     103; 
      ==I   ; 
      A     #s_Message_Received; 
      JCN   DA99; 

      CALL #s_FB_Rcv_CapacityInstr (
           i_ANY_RCV_Msg_Body       := #s_Received_Message);

DA99: NOP   0; 
NETWORK
TITLE =EA: EmptyLaneInstruction ('104')

      L     #s_Received_Message.MessageID; 
      L     104; 
      ==I   ; 
      A     #s_Message_Received; 
      JCN   EA99; 

      CALL "FC_Clear_Bool_DB" (
           i_TableDB                := "DB_EmptyLaneTable");

      CALL #s_FB_EmtyLaneInstr (
           i_ANY_RCV_Msg_Body       := #s_Received_Message);

EA99: NOP   0; 
NETWORK
TITLE =FA: ItemRequest ('701') from external system 2 (TFC)

      L     #s_Received_Message.MessageID; 
      L     701; 
      ==I   ; // IF Message ID is correct
      A     #s_Message_Received; 
      JCN   FA99; 

      L     #s_Received_Message.AreaID; 
      L     #i_CFG_AreaID; 
      ==I   ; 
      JCN   FA99; 

      CALL #s_FB_Rcv_ItemRequest (
           i_ANY_RCV_Msg_Body       := #s_Received_Message);

      BEU   ; 

FA99: NOP   0; 
NETWORK
TITLE =GA: Rel Ctrl Table Update ('702') from external system 2 (TFC)

      L     #s_Received_Message.MessageID; 
      L     702; 
      ==I   ; // IF Message ID is correct
      A     #s_Message_Received; 
      JCN   GA99; 

      CALL #s_FB_Rcv_RelCtrlTabUp (
           i_ANY_RCV_Msg_Body       := #s_Received_Message,
           i_DB_Event_Num           := #t_Event_DB_Num);

      BEU   ; 

GA99: NOP   0; 
NETWORK
TITLE =HA: Sort Ctrl Table Update ('703') from external system 2 (TFC)

      L     #s_Received_Message.MessageID; 
      L     703; 
      ==I   ; // IF Message ID is correct
      A     #s_Message_Received; 
      JCN   HA99; 

      CALL #s_FB_Rcv_SrtCtrlTabUp (
           i_ANY_RCV_Msg_Body       := #s_Received_Message,
           i_DB_Event_Num           := #t_Event_DB_Num);

      BEU   ; 

HA99: NOP   0; 
NETWORK
TITLE =IA: ICS MaintenanceRequest

      L     #s_Received_Message.MessageID; 
      L     2901; 
      ==I   ; // IF Message ID is correct
      A     #s_Message_Received; 
      JCN   IA99; 

      CALL #s_FB_Rcv_ICSMaintReq (
           i_ANY_RCV_Msg_Body       := #s_Received_Message);

      BEU   ; 

IA99: NOP   0; 

NETWORK
TITLE =JA: ICS MaintenanceReply

      L     #s_Received_Message.MessageID; 
      L     2902; 
      ==I   ; // IF Message ID is correct
      A     #s_Message_Received; 
      JCN   JA99; 

      CALL #s_FB_Rcv_ICSMaintRep (
           i_ANY_RCV_Msg_Body       := #s_Received_Message);

      BEU   ; 

JA99: NOP   0; 

NETWORK
TITLE =KA: LifeSignResponse TCF ('12')

      CALL #s_FB_LS_TFC (
           i_ANY_RCV_Msg_Body       := #s_Received_Message,
           i_CFG_LifeSignReq_Interv := #i_CFG_LifeSignReq_Interv,
           i_CFG_LifeSignResp_Time  := #i_CFG_LifeSignResp_Time,
           i_CFG_NumLifeSgnMiss_Err := #i_CFG_NumLifeSgnMiss_Err,
           i_CFG_AreaID             := #i_CFG_AreaID,
           i_CFG_LifeSignReq_MsgID  := 11,
           i_CFG_LifeSignResp_MsgID := 12,
           i_InitSync_MsgID         := 13,
           i_DB_Num_Send_FIFO       := #s_Send_FIFO_DBNr,
           i_Message_Received       := #s_Message_Received,
           i_Degraded_Mode          := #i_CMD_Degraded_Mode,
           o_Err_No_Lifesigns       := #o_Err_No_Lifesigns_TFC);

END_FUNCTION_BLOCK

