FUNCTION_BLOCK "FB_MTZ01_F"
TITLE =%version: 1.08 % CN: 55
//Function:
//- Fixed/control part of MakeUp Triplaner Zone type 01 (MTZ01)
//
//History:
//This version  %created_by:  nluhl %
//              %date_created:  Monday, August 06, 2018 11:14:52 AM %
//              %release: VI_RAW/RA_Dev %
//
//Modification History:                                   By:    Date:     Rev:
//----------------------------------------------------  -------  --------  -----
//ra#29694: Corrected labels and LAD networks           nluhl    07-08-18  1.08
//ra#29694: Remove startup condition for light/beeper   nluhl    26-06-18  1.07
//ra#24159: Remove Signal light active on error         nlHdL    16-10-17  1.06
//ra#21256: Startup signal light constant output        nlHdL    02-08-17  1.05
//ra#18176: Halt signal too late on section start       nlDY     25-10-16  1.04
//ra#18163: Beeper activates when coming out of Esave   nlDY     25-10-16  1.04
//ra#17941: Beeper not activated at the correct moment  nlDY     19-10-16  1.03
//ra#16817: o_IO_SL_Available always false              nlDY     08-09-16  1.02
//ra#13938: Initial version                             nlJClo   19-05-16  1.01
//
//
//Description: 
//Fixed part of MTZ containing the SLC and start up.
//
//
//
FAMILY : RAW
VERSION : 0.0


VAR_INPUT
  i_CMD_Reset : BOOL ;	//CMD: Reset command
  i_Available_IO : BOOL  := TRUE;	//Signal for indication IO is OK (no Fieldbus or IO error)
  i_Safety_OK : BOOL  := TRUE;	//Safety is OK signal
  i_TRS1_DB_PLT : BLOCK_DB ;	//Data Structure: Position DataBlock TRS1
  i_TRS1_CFG_StartUp_Time : INT  := 5000;	//CFG: [ms] start up time
  i_TRS1_Operation_On : BOOL ;	// Operational on
  i_TRS1_Ext_Req_Rst_ESve : BOOL ;	// Extern request to reset energysave
  i_TRS1_Ext_Req_Stop : BOOL ;	// Extern request to stop
  i_TRS1_ST_Started : BOOL ;	//ST: Started
  i_SLC_ASI_NotPrsnt : BOOL ;	//ASI: Slave is not Present
  i_SLC_ASI_Err : BOOL ;	//ASI: Slave has an error
  i_MTZ_ST_Error : BOOL ;	//MTZ status error
  i_ST_Full_level_4 : BOOL ;	//ST: Carrousel full level 4 reached
  i_ST_Full_Error : BOOL ;	//ST: Carrousel full level 4 reached error
END_VAR
VAR_OUTPUT
  o_SLC_ST_ASI_NotPrsnt : BOOL ;	//ST: ASI slave is not present
  o_SLC_ST_ASI_Error : BOOL ;	//ST: ASI slave has an error
  o_StrtUp_Signal_Req_Halt : BOOL ;	//Request halt
  o_IO_FL_Signal_Lamp : BOOL ;	//HW: Flashlight 
  o_IO_Horn_Start_Up : BOOL ;	//HW: Horn when starting up this level
  o_IO_SL_Available : BOOL ;	//HW: Signal Light Available
END_VAR
VAR
  s_SLC_FB_HW_ASI : "FB_HW_ASi";	//Signal light Coloumn ASI Slave hardware layer
  s_TRS1_FB_StrtUp_Signal : "FB_Start_Up_Signal";	
  s_FL_Start_Up : BOOL ;	//Request flashlight start up
  s_Horn_Start_Up : BOOL ;	//Request Horn start up	
  s_TRS1_FL_Start_Up : BOOL ;	//Stat up signal	
  s_TRS1_Horn_Start_Up : BOOL ;	//Request horn start up
  s_TRS1_Horn_Full : BOOL ;	//Request horn to indicate a full carousel
  s_FP_Full_level_4 : BOOL ;	//Positive flank for full level 4
  s_SL_Available : BOOL ;	//HW: Signal Light Available
  s_MTZ_Inactive : BOOL ;	//MTZ is inactive
  s_StrtUp_Signal_Req_Halt : BOOL ;	//Request halt
END_VAR
VAR_TEMP
  t_BOOL_Not_Used : BOOL ;	
  t_DB_Num_Event : INT ;	//DB Event Number
END_VAR
BEGIN
NETWORK
TITLE =AA: MTZ Active/Inactive
// MTZ Active
      A     #i_Safety_OK; 
      A     #i_TRS1_ST_Started; 
      AN    #i_MTZ_ST_Error; 
      R     #s_MTZ_Inactive; 

// MTZ Inactive
      AN    #i_Safety_OK; 
      ON    #i_TRS1_ST_Started; 
      S     #s_MTZ_Inactive; 

NETWORK
TITLE =BA: Triplanner Start-up signal

      OPN   "DB_Event_Section"; 
      L     DBNO; 
      T     #t_DB_Num_Event; 

      CALL #s_TRS1_FB_StrtUp_Signal (
           i_DB_PLT                 := #i_TRS1_DB_PLT,
           i_DB_Event_Num           := #t_DB_Num_Event,
           i_CFG_StartUp_Time       := #i_TRS1_CFG_StartUp_Time,
           i_CFG_No_Startup_E_Save  := TRUE,
           i_Operational_On         := #i_TRS1_Operation_On,
           i_Reset_E_Save           := #i_TRS1_Ext_Req_Rst_ESve,
           o_FL_Start_Up            := #s_TRS1_FL_Start_Up,
           o_Horn_Start_Up          := #s_TRS1_Horn_Start_Up,
           o_Req_Halt               := #s_StrtUp_Signal_Req_Halt);

      A     #s_StrtUp_Signal_Req_Halt; 
      ON    #i_TRS1_Operation_On; 
      =     #o_StrtUp_Signal_Req_Halt; 

NETWORK
TITLE =CA: Available signal light if MTZ is Available or in Error
////LAD!
      AN    #s_MTZ_Inactive; 
      AN    #i_TRS1_Ext_Req_Stop; 
      O     ; 
      A     #i_MTZ_ST_Error; 
      A     "M_System_Clock_1.0_sec."; 
      =     #s_SL_Available; 
NETWORK
TITLE =DA: Flash light - MTZ no longer uses Startup signal
//LAD!
      O     #i_ST_Full_level_4; 
      =     #s_FL_Start_Up; 
NETWORK
TITLE =EA:  Horn signalling - MTZ no longer uses Startup signal
//LAD!
      A(    ; 
      A     #i_ST_Full_level_4; 
      FP    #s_FP_Full_level_4; 
      S     #s_TRS1_Horn_Full; 
      A(    ; 
      O     #i_CMD_Reset; 
      O     #i_ST_Full_Error; 
      ON    #i_ST_Full_level_4; 
      )     ; 
      R     #s_TRS1_Horn_Full; 
      A     #s_TRS1_Horn_Full; 
      )     ; 
      A     "M_System_Clock_1.0_sec."; 
      =     #s_Horn_Start_Up; 

NETWORK
TITLE =FA: Hardware layer for Signal Light Coloumn
//--Hardware layer for Signal Light Coloumn
      CALL #s_SLC_FB_HW_ASI (
           i_Req_Output_1           := #s_FL_Start_Up,
           i_Req_Output_2           := #s_Horn_Start_Up,
           i_Req_Output_3           := #s_SL_Available,
           i_Req_Output_4           := FALSE,
           i_CMD_Reset              := #i_CMD_Reset,
           i_Available_IO           := #i_Available_IO,
           i_ASI_NotPrsnt           := #i_SLC_ASI_NotPrsnt,
           i_Slave_Error            := #i_SLC_ASI_Err,
           i_CFG_Delay_Time         := 0,
           o_ST_ASI_NotPrsnt        := #o_SLC_ST_ASI_NotPrsnt,
           o_ST_ASI_Error           := #o_SLC_ST_ASI_Error,
           o_IO_Output_1            := #o_IO_FL_Signal_Lamp,
           o_IO_Output_2            := #o_IO_Horn_Start_Up,
           o_IO_Output_3            := #o_IO_SL_Available,
           o_IO_Output_4            := #t_BOOL_Not_Used);

NETWORK
TITLE =GA: Clearing the Negative value from TT/TF PLT-DB
// when the Bag reached below the zero position clear from the PLT_DB
      OPN   #i_TRS1_DB_PLT; 
      L     DBNO; 

      L     DBW    0; 
      L     0; 
      >I    ; 
      JCN   GA99; 

      L     DBW    0; 
      L     1; 
      -I    ; 
      L     8; //8 Bytes per Entry
      *I    ; 
      L     14; //Header
      +I    ; 
      SLW   3; 
      LAR1  ; 

      L     DBW [AR1,P#0.0]; 
      L     0; 
      <I    ; 
      JCN   GA99; 

      L     0; 
      T     DBW [AR1,P#0.0]; 
      T     DBW [AR1,P#2.0]; 
      T     DBW [AR1,P#4.0]; 
      T     DBW [AR1,P#6.0]; 

      L     DBW    0; 
      L     1; 
      -I    ; 
      T     DBW    0; 

GA99: NOP   0; 

END_FUNCTION_BLOCK

